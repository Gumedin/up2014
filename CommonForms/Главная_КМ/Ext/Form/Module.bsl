&НаКлиенте
Процедура ПриОткрытии(Отказ)
	ПодготовитьУсловияОтбора();
	Обновить(Неопределено);
КонецПроцедуры

&НаСервере
Процедура ПодготовитьУсловияОтбора()
	Если Год = 0 Тогда
		Год = Год( ТекущаяДата() );
	КонецЕсли;
	
//	Статус.Очистить();
	
	Если Статус.Количество() = 0 Тогда
		Для Каждого с Из Перечисления.СтатусыПроектов Цикл
			стр = Статус.Добавить();
			стр.Статус = с;
			стр.Установлен = Истина;
		КонецЦикла;
	КонецЕсли;
	
//	СтатусЗадач.Очистить();
	
	Если СтатусЗадач.Количество() = 0 Тогда
		Для Каждого с Из Перечисления.СтатусыЗадачПроектов Цикл
			стр = СтатусЗадач.Добавить();
			стр.Статус = с;
			стр.Установлен = Истина;
		КонецЦикла;
	КонецЕсли;
	
	КМ.Очистить();
	аКМ = МассивКлиентМенджеров(Год);
	Для Каждого с Из аКМ Цикл
		стр = КМ.Добавить();
		стр.КМ = с;
		стр.Установлен = Истина;
	КонецЦикла;
КонецПроцедуры	

&НаСервере
Процедура ОбновитьМоиПроекты()
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	Д.Проект КАК Проект,
		|	Д.Статус КАК Статус,
		|	Д.СтатусЗадач КАК СтатусЗадач,
		|	Д.Год КАК Год,
		|	Д.КлиентМенеджер КАК КлиентМенеджер,
		|	СУММА(Д.Обеспечено) КАК Обеспечено,
		|	СУММА(Д.Реализация) КАК Реализация,
		|	СУММА(Д.Оплата) КАК Оплата,
		|	СУММА(Д.ООбеспечено) КАК ООбеспечено,
		|	СУММА(Д.ОГарантия) КАК ОГарантия,
		|	СУММА(Д.ОПрогноз) КАК ОПрогноз
		|ИЗ
		|	(ВЫБРАТЬ
		|		Проекты.Ссылка КАК Проект,
		|		Проекты.Статус КАК Статус,
		|		Проекты.СтатусЗадач КАК СтатусЗадач,
		|		ЗадачиПроектов.ГодЗадачи КАК Год,
		|		Проекты.МенеджерПроекта КАК КлиентМенеджер,
		|		БюджетПоМесяцам.Сумма КАК Обеспечено,
		|		0 КАК Реализация,
		|		0 КАК Оплата,
		|		ВЫБОР
		|			КОГДА БюджетПоМесяцам.ВидПлана = ЗНАЧЕНИЕ(Перечисление.ВидыПланаБюджета.Обеспечено)
		|				ТОГДА БюджетПоМесяцам.Сумма
		|			ИНАЧЕ 0
		|		КОНЕЦ КАК ООбеспечено,
		|		ВЫБОР
		|			КОГДА БюджетПоМесяцам.ВидПлана = ЗНАЧЕНИЕ(Перечисление.ВидыПланаБюджета.Гарантия)
		|				ТОГДА БюджетПоМесяцам.Сумма
		|			ИНАЧЕ 0
		|		КОНЕЦ КАК ОГарантия,
		|		ВЫБОР
		|			КОГДА БюджетПоМесяцам.ВидПлана = ЗНАЧЕНИЕ(Перечисление.ВидыПланаБюджета.Прогноз)
		|				ТОГДА БюджетПоМесяцам.Сумма
		|			ИНАЧЕ 0
		|		КОНЕЦ КАК ОПрогноз
		|	ИЗ
		|		Справочник.Проекты КАК Проекты,
		|		Справочник.ЗадачиПроектов КАК ЗадачиПроектов,
		|		РегистрНакопления.БюджетПоМесяцам КАК БюджетПоМесяцам
		|	ГДЕ
		|		Проекты.МенеджерПроекта В(&КМ)
		|		И ЗадачиПроектов.Владелец = Проекты.Ссылка
		|		И ЗадачиПроектов.ГодЗадачи В(&Год)
		|		И Проекты.Статус В(&СтатусПроекта)
		|		И Проекты.СтатусЗадач В(&СтатусЗадач)
		|		И БюджетПоМесяцам.ЗадачаПроекта = ЗадачиПроектов.Ссылка
		|		И БюджетПоМесяцам.СтатьяСметы = ЗНАЧЕНИЕ(Справочник.СтатьиСметы.ДохФинансовые)
		|		И БюджетПоМесяцам.ТипСуммы = ЗНАЧЕНИЕ(Перечисление.ТипСуммыБюджета.План)
		|	
		|	ОБЪЕДИНИТЬ ВСЕ
		|	
		|	ВЫБРАТЬ
		|		Проекты.Ссылка,
		|		Проекты.Статус,
		|		Проекты.СтатусЗадач,
		|		ЗадачиПроектов.ГодЗадачи,
		|		Проекты.МенеджерПроекта,
		|		0,
		|		РеализацияОбороты.ФактОборот,
		|		0,
		|		0,
		|		0,
		|		0
		|	ИЗ
		|		Справочник.Проекты КАК Проекты,
		|		Справочник.ЗадачиПроектов КАК ЗадачиПроектов,
		|		РегистрНакопления.Реализация.Обороты КАК РеализацияОбороты
		|	ГДЕ
		|		Проекты.МенеджерПроекта В(&КМ)
		|		И ЗадачиПроектов.Владелец = Проекты.Ссылка
		|		И ЗадачиПроектов.ГодЗадачи В(&Год)
		|		И Проекты.Статус В(&СтатусПроекта)
		|		И Проекты.СтатусЗадач В(&СтатусЗадач)
		|		И РеализацияОбороты.ЗадачаПроекта = ЗадачиПроектов.Ссылка
		|	
		|	ОБЪЕДИНИТЬ ВСЕ
		|	
		|	ВЫБРАТЬ
		|		Проекты.Ссылка,
		|		Проекты.Статус,
		|		Проекты.СтатусЗадач,
		|		ЗадачиПроектов.ГодЗадачи,
		|		Проекты.МенеджерПроекта,
		|		0,
		|		0,
		|		ОбеспечениеОплатаОбороты.СуммаОплаченоОборот,
		|		0,
		|		0,
		|		0
		|	ИЗ
		|		Справочник.Проекты КАК Проекты,
		|		Справочник.ЗадачиПроектов КАК ЗадачиПроектов,
		|		РегистрНакопления.ОбеспечениеОплата.Обороты КАК ОбеспечениеОплатаОбороты
		|	ГДЕ
		|		Проекты.МенеджерПроекта В(&КМ)
		|		И ЗадачиПроектов.Владелец = Проекты.Ссылка
		|		И ЗадачиПроектов.ГодЗадачи В(&Год)
		|		И Проекты.Статус В(&СтатусПроекта)
		|		И Проекты.СтатусЗадач В(&СтатусЗадач)
		|		И ОбеспечениеОплатаОбороты.ЗадачаПроекта = ЗадачиПроектов.Ссылка) КАК Д
		|
		|СГРУППИРОВАТЬ ПО
		|	Д.Проект,
		|	Д.Статус,
		|	Д.СтатусЗадач,
		|	Д.Год,
		|	Д.КлиентМенеджер";

	Запрос.УстановитьПараметр("Год", Год);
	
	Отбор = Новый Структура();
	Отбор.Вставить("Установлен", Истина);
	
	фКМ = КМ.Выгрузить(Отбор, "КМ");
	фСтатусП = Статус.Выгрузить(Отбор, "Статус");
	фСтатусЗ = СтатусЗадач.Выгрузить(Отбор, "Статус");
	
	Запрос.УстановитьПараметр("КМ", фКМ);
	Запрос.УстановитьПараметр("СтатусПроекта", фСтатусП);
	Запрос.УстановитьПараметр("СтатусЗадач", фСтатусЗ);
	
	Результат = Запрос.Выполнить().Выгрузить();
	МоиПроекты.Загрузить(Результат);	
КонецПроцедуры	

&НаКлиенте
Процедура Обновить(Команда)
	ОбновитьМоиПроекты();
	СтатистикаДляКМ = А_Аналитика.СтатистикаДляКМ("СтатистикаДляКМ", МоиПроекты);
КонецПроцедуры

&НаКлиенте
Процедура ГодОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	Год = ВыбранноеЗначение;
	Обновить(Неопределено);
	СтандартнаяОбработка = Ложь;
КонецПроцедуры

&НаКлиенте
Процедура ГодОкончаниеВводаТекста(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, СтандартнаяОбработка)
	Год = Текст;
	Обновить(Неопределено);
	СтандартнаяОбработка = Ложь;
КонецПроцедуры

&НаКлиенте
Процедура ОтборПриИзменении(Элемент)
	Обновить(Неопределено);
КонецПроцедуры

&НаКлиенте
Процедура МоиПроектыВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	//Показать выбранный проект
	ПоказатьЗначение(,Элемент.ТекущиеДанные.Проект);
КонецПроцедуры

&НаКлиенте
Процедура МоиПроектыПриАктивизацииСтроки(Элемент)	
	//ОбновитьСтраницу(Элемент.ВыделенныеСтроки);
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьРедакторHTML(Команда)
	РедакторКод = "СтатистикаДляКМ";
	РедакторHTMLДокумента = А_Аналитика.ПодготовитьHTMLСтраницу(РедакторКод);
	Элементы.РедакторШаблона.Видимость = Истина;
КонецПроцедуры

&НаКлиенте
Процедура Записать(Команда)
	А_Аналитика.ЗаписатьHTMLСтраницу(РедакторКод, РедакторHTMLДокумента);
	Обновить(Неопределено);
//	Элементы.РедакторШаблона.Видимость = Ложь;
КонецПроцедуры

&НаКлиенте
Процедура КМ_ВсеУстановить(Команда)
	Для Каждого строка Из КМ Цикл 
		строка.Установлен = Истина;
	КонецЦикла;
	Обновить(Неопределено);
КонецПроцедуры

&НаКлиенте
Процедура КМ_ВсеСнять(Команда)
	Для Каждого строка Из КМ Цикл 
		строка.Установлен = Ложь;
	КонецЦикла;
	Обновить(Неопределено);
КонецПроцедуры

&НаКлиенте
Процедура КМ_ВсеПоменять(Команда)
	Для Каждого строка Из КМ Цикл 
		строка.Установлен = Не строка.Установлен;
	КонецЦикла;
	Обновить(Неопределено);
КонецПроцедуры
