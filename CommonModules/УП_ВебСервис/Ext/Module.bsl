Процедура ВыполнитьОперацию( ИмяWSСсылки, 
							 ИмяОперации, 
							 ПараметрыОперации,
							 ПараметрыПодключения = Неопределено,
							 Результат) Экспорт 

							 
	ОписаниеСервиса		= WSСсылки[ИмяWSСсылки].ПолучитьWSОпределения().Сервисы.Получить(0);		
	ИмяСервиса			= ОписаниеСервиса.Имя;
	
	
	// коллекция
	ОписанияОпераций	= ОписаниеСервиса.ТочкиПодключения[0].Интерфейс.Операции;
	ПоследнийИндекс		= ОписанияОпераций.Количество()-1;
	
	Для Индекс = 0 По ПоследнийИндекс Цикл
		ОписаниеОперации = ОписанияОпераций[Индекс];
		Если ОписаниеОперации.Имя = ИмяОперации Тогда
			Прервать;
		КонецЕсли;
		ОписаниеОперации = Неопределено;
	КонецЦикла;
	Если ОписаниеОперации = Неопределено Тогда
		Возврат; // имя операции не обнаружено
	КонецЕсли;
	
	// операцию нашли
	ОписанияПараметров 	= ОписаниеОперации.Параметры;
	ПоследнийИндекс		= ОписанияПараметров.Количество()-1;
	СтрокаПараметров 	= "";
	
	Для Индекс = 0 По ПоследнийИндекс Цикл
		ОписаниеПараметра = ОписанияПараметров[Индекс];
		Если ПараметрыОперации.Свойство( ОписаниеПараметра.Имя ) = Истина Тогда
			
			СтрокаПараметров = СтрокаПараметров  + 
								?(СтрокаПараметров="","",",") +
								"ПараметрыОперации."+ ОписаниеПараметра.Имя;
								
		Иначе
			СтрокаПараметров = СтрокаПараметров  + 
								?(СтрокаПараметров="","",",");
								
		КонецЕсли;
	
	КонецЦикла;
	
	
	
	URIПространстваИменСервиса  = ОписаниеСервиса.URIПространстваИмен;
	ИмяПорта					= ОписаниеСервиса.ТочкиПодключения[0].Имя;
	
	Соединение 		=
	WSСсылки[ИмяWSСсылки].СоздатьWSПрокси(URIПространстваИменСервиса,ИмяСервиса,ИмяПорта);			
	
	Если ПараметрыПодключения <> Неопределено Тогда
		Соединение.Пользователь = ПараметрыПодключения.Пользователь;
		Соединение.Пароль		= ПараметрыПодключения.Пароль;
	КонецЕсли;
	
	//Результат = Соединение.Регистрация(Пользователь, Пароль, ИмяРоли );
	
	Результат = Вычислить("Соединение." + ИмяОперации +"(" + СтрокаПараметров + ")");

КонецПроцедуры// ВыполнитьОперацию()()
