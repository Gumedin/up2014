
Функция ПолучитьТоварПоИсточнику( Источник, Ссылка )
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ЗакупкаППЛОТовар.Ссылка,
		|	ЗакупкаППЛОТовар.НомерСтроки,
		|	ЗакупкаППЛОТовар.Номенклатура,
		|	ЗакупкаППЛОТовар.Количество,
		|	ЗакупкаППЛОТовар.Цена,
		|	ЗакупкаППЛОТовар.СуммаПП,
		|	ЗакупкаППЛОТовар.СуммаПравоОбл,
		|	ЗакупкаППЛОТовар.СуммаДоход,
		|	ЗакупкаППЛОТовар.Скидка
		|ИЗ
		|	"+ Источник + " КАК ЗакупкаППЛОТовар
		|ГДЕ
		|	ЗакупкаППЛОТовар.Ссылка = &Ссылка";
		
		
	Запрос.УстановитьПараметр("Ссылка", Ссылка );
	РезультатЗапроса = Запрос.Выполнить().Выгрузить();
	Возврат РезультатЗапроса;

КонецФункции

Процедура СформироватьРасчетСтоимости(ТабДок, Ссылка, Макет )
	
	
	// начальный фронт работ
	Если 		ТипЗнч( Ссылка )= Тип("ДокументСсылка.ЗакупкаППЛО" ) Тогда
		Источник 		= "Документ.ЗакупкаППЛО.Товар";
		Исполнитель		= Ссылка.ИсполнительДокумента;
		Проект	 		= Ссылка.ЗадачаПроекта.Владелец;
		ЗадачаПроекта 	= Ссылка.ЗадачаПроекта;
		Заголовок		= "Расчет стоимости ПП(ЛО) № " + Ссылка.Номер + " от " + Формат(Ссылка.Дата,"ДЛФ=D"); 
		
	ИначеЕсли 	ТипЗнч( Ссылка )= Тип("СправочникСсылка.КалькуляторСтоимостиППЛО" ) Тогда
		Источник 		= "Справочник.КалькуляторСтоимостиППЛО.Товар";
		Исполнитель		= Ссылка.Исполнитель;
		Проект	 		= Ссылка.Родитель.Наименование; 
		ЗадачаПроекта 	= Ссылка.Наименование;
		Заголовок		= "Калькулятор стоимости ПП(ЛО)";
		
	КонецЕсли;
	
	тзТовар = ПолучитьТоварПоИсточнику( Источник, Ссылка );
	//
	Шапка = Макет.ПолучитьОбласть("Шапка");
	ЗаполнитьЗначенияСвойств( Шапка.Параметры, Ссылка );
	Шапка.Параметры.Заголовок 		= Заголовок;
	Шапка.Параметры.ЗадачаПроекта 	= ЗадачаПроекта;
	Шапка.Параметры.Проект 			= Проект;
	//
	ТабДок.Вывести(Шапка );
	Для Каждого СтрТовар ИЗ тзТовар Цикл
		ОблТовар = Макет.ПолучитьОбласть("Товар");
		ЗаполнитьЗначенияСвойств( ОблТовар.Параметры, СтрТовар );
		ТабДок.Вывести( ОблТовар );
	КонецЦикла;
	
	тзТовар.Свернуть("","Количество,СуммаПП,СуммаПравоОбл,СуммаДоход");
	Подвал = Макет.ПолучитьОбласть("Подвал");
	ЗаполнитьЗначенияСвойств( Подвал.Параметры, Ссылка );
	Если тзТовар.Количество() <> 0 Тогда
		ЗаполнитьЗначенияСвойств( Подвал.Параметры, тзТовар[0] );
		Подвал.Параметры.ИтогоВендору 		= тзТовар.Итог("СуммаПравоОбл") - Ссылка.СуммаДопОтВендора;
		Подвал.Параметры.ИтогоОтКлиента 	= Ссылка.СуммаДопОтКлиента + тзТовар.Итог("СуммаПП");
		Подвал.Параметры.ИтогоДоход			= Подвал.Параметры.ИтогоОтКлиента - Подвал.Параметры.ИтогоВендору;
	КонецЕсли;
	Подвал.Параметры.Исполнитель		= Исполнитель;
	
	ТабДок.Вывести( Подвал );
	ТабДок.АвтоМасштаб 			= Истина;
	ТабДок.ОриентацияСтраницы 	= ОриентацияСтраницы.Ландшафт;
	
КонецПроцедуры


// Процедура печати документа.
//
Функция ПечатьСтоимостиППЛО(МассивОбъектов, ОбъектыПечати, ИмяМакета = "СтоимостьППЛО") Экспорт
	Запрос = Новый Запрос;

	Запрос.Текст = 
		"ВЫБРАТЬ
		|	РасчетСтоимости.Ссылка
		|ИЗ
		|	Документ.ЗакупкаППЛО КАК РасчетСтоимости
		|ГДЕ
		|	РасчетСтоимости.Ссылка В(&МассивОбъектов)
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	Калькулятор.Ссылка
		|ИЗ
		|	Справочник.КалькуляторСтоимостиППЛО КАК Калькулятор
		|ГДЕ
		|	Калькулятор.Ссылка В(&МассивОбъектов)";
	
	
	Запрос.УстановитьПараметр("МассивОбъектов", МассивОбъектов);
	Шапка = Запрос.Выполнить().Выбрать();
	
	ТабличныйДокумент = Новый ТабличныйДокумент;
	ТабличныйДокумент.КлючПараметровПечати = "СтоимостьППЛО";
	
	Макет = УправлениеПечатью.ПолучитьМакет("ОбщиеМакеты.ПФ_MXL_СтоимостьППЛО");
	
	Пока Шапка.Следующий() Цикл
		Если ТабличныйДокумент.ВысотаТаблицы > 0 Тогда
			ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
		КонецЕсли;
		
		НомерСтрокиНачало = ТабличныйДокумент.ВысотаТаблицы + 1;
		
		// выводим документ
		//
		СформироватьРасчетСтоимости(ТабличныйДокумент, Шапка.Ссылка, Макет );
		
		// конец вывода документа

		УправлениеПечатью.ЗадатьОбластьПечатиДокумента(ТабличныйДокумент, НомерСтрокиНачало, ОбъектыПечати, Шапка.Ссылка);
	
	КонецЦикла;
	
	Возврат ТабличныйДокумент;
	
КонецФункции
