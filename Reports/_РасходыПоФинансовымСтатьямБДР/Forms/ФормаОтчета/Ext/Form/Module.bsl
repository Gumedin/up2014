
#Область ФормированиеОтчета

&НаСервереБезКонтекста
Функция КоличествоМесяцевВПериоде( Период )
	КоличествоМесяцев = 1;
	Д1 = НачалоМесяца( Период.ДатаНачала );
	Пока Д1 < НачалоМесяца( Период.ДатаОкончания ) Цикл 
		Д1 = ДобавитьМесяц( Д1, 1 );
		КоличествоМесяцев = КоличествоМесяцев + 1;
	КонецЦикла;
	Возврат КоличествоМесяцев;
КонецФункции

&НаСервере
Процедура ВывестиШапку( ТабДок, Макет, НачалоПериода, КоличествоМесяцев )
	ОблН = Макет.ПолучитьОбласть( "Заголовок|Начало");
	ОблН.Параметры.ПредставлениеПериода = ПредставлениеПериода( НачалоПериода, 
																КонецМесяца(ДобавитьМесяц( НачалоПериода, КоличествоМесяцев-1)), "ФП = Истина ");
	ОблН.Параметры.ГодБюджета = Формат( ГодБюджета, "ЧН=0; ЧГ=");
	ТабДок.Вывести( ОблН );
	
	Для Н = 1 ПО КоличествоМесяцев Цикл
		ОблМ = Макет.ПолучитьОбласть( "Заголовок|ЗаМесяц");
		ОблМ.Параметры.ТипСуммы	= ТипСуммыБюджета;
		ОблМ.Параметры.Месяц 	= ДобавитьМесяц( НачалоПериода, Н-1);
		ТабДок.Присоединить( ОблМ );
	КонецЦикла;
КонецПроцедуры

&НаСервере
Процедура ВывестиПоОснованию( ТабДок, Макет, НачалоПериода, КоличествоМесяцев, СтрОснование )
	ОблН = Макет.ПолучитьОбласть( "Основание|Начало");
	ЗаполнитьЗначенияСвойств( ОблН.Параметры, СтрОснование );
	ТабДок.Вывести( ОблН, 2 );
	
	Для Н = 1 ПО КоличествоМесяцев Цикл
		ОблМ = Макет.ПолучитьОбласть( "Основание|ЗаМесяц");
		ОблМ.Параметры.Сумма = СтрОснование["Сумма" + Н ];
		ТабДок.Присоединить( ОблМ );
	КонецЦикла;
КонецПроцедуры

&НаСервере
Процедура ВывестиПоСтатье( ТабДок, Макет, НачалоПериода, КоличествоМесяцев, Статья, тзРезультат )
	ОблН = Макет.ПолучитьОбласть( "Статья|Начало");
	ОблН.Параметры.КодСтатьи 	= Статья.Значение;
	ОблН.Параметры.Статья 		= Статья.Представление;
	ОблН.Параметры.Сумма		= тзРезультат.Итог( "Сумма" );
	ТабДок.Вывести( ОблН, 1 );
	
	Для Н = 1 ПО КоличествоМесяцев Цикл
		ОблМ = Макет.ПолучитьОбласть( "Статья|ЗаМесяц");
		ОблМ.Параметры.Сумма = тзРезультат.Итог( "Сумма" + Н );
		ТабДок.Присоединить( ОблМ );
	КонецЦикла;
	
	// 
	Если ВыводитьОснования Тогда

		Для Каждого СтрОснование ИЗ тзРезультат Цикл
			ВывестиПоОснованию( ТабДок, Макет, НачалоПериода, КоличествоМесяцев, СтрОснование );
		КонецЦикла;
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура СформироватьНаСервере( ТабДок )
	НачалоПериода 		= НачалоМесяца( Период.ДатаНачала );
	КоличествоМесяцев 	= КоличествоМесяцевВПериоде( Период );
	
	Макет 				= РеквизитФормыВЗначение("Отчет").ПолучитьМакет("Макет");
	ВывестиШапку( ТабДок, Макет, НачалоПериода, КоличествоМесяцев );
	
	ТабДок.НачатьАвтогруппировкуСтрок();

	Для Каждого Эл ИЗ ФинансовыеСтатьи Цикл
		Если Эл.Пометка Тогда
			// массив по основаниям
			Если ТипСуммыБюджета = Перечисления.ТипСуммыБюджета.План Тогда
				тзРезультат = УП_ДанныеДляБюджета.ПолучитьПланПоСтатье( ГодБюджета, Эл.Значение, НачалоПериода, КоличествоМесяцев );
			Иначе
				тзРезультат = УП_ДанныеДляБюджета.ПолучитьФактПоСтатье( ГодБюджета, Эл.Значение, НачалоПериода, КоличествоМесяцев );
			КонецЕсли;
			ВывестиПоСтатье( ТабДок, Макет, НачалоПериода, КоличествоМесяцев, Эл, тзРезультат );
			
		КонецЕсли;
		
	КонецЦикла;
	
	ТабДок.ЗакончитьАвтогруппировкуСтрок();
	
КонецПроцедуры

&НаКлиенте
Процедура Сформировать(Команда)
	КоличествоМесяцев 	= КоличествоМесяцевВПериоде( Период );
	Если КоличествоМесяцев > 12 Тогда
		ПоказатьПредупреждение(,"В периоде может быть не больше 12 месяцев",10);
		Возврат;
	КонецЕсли;
	
	ТабличныйДокумент = Новый ТабличныйДокумент;
	СформироватьНаСервере(ТабличныйДокумент);
	
	ТабличныйДокумент.АвтоМасштаб 			= Истина;
	ТабличныйДокумент.ОриентацияСтраницы 	= ОриентацияСтраницы.Ландшафт;
	ТабличныйДокумент.ФиксацияСверху		= 7;
	ТабличныйДокумент.ФиксацияСлева			= 5;
	ТабличныйДокумент.Защита				= Истина;
	ТабличныйДокумент.Показать("Расходы по статьям, " + ТипСуммыБюджета);
	
	
КонецПроцедуры

#КонецОбласти


#Область ОбработчикиФормы
&НаСервере
Процедура ЗаполнитьСписокСтатей()
	ФинансовыеСтатьи.Очистить();
	Если РольДоступна(Метаданные.Роли.ПолныеПрава )
	или	 РольДоступна( Метаданные.Роли.ФинансовыйМенеджер )	Тогда
		ФинансовыеСтатьи.Добавить("002.0301", "Вендер_ПП/ЛО/ИТС");
		ФинансовыеСтатьи.Добавить("002.0302", "Командировочные расходы");
		ФинансовыеСтатьи.Добавить("002.0303", "Компенсация за удаленного клиента");
		ФинансовыеСтатьи.Добавить("002.0304", "Подарки");
		ФинансовыеСтатьи.Добавить("002.0305", "Прочие расходы");
		ФинансовыеСтатьи.Добавить("002.0401", "Субподряд (производство)");
		ФинансовыеСтатьи.Добавить("002.0101", "Фонд КМ (продажи)");
		ФинансовыеСтатьи.Добавить("002.0102", "Расходы по заключению договора");
		ФинансовыеСтатьи.Добавить("002.0201", "Фонд Произв.перс.");
		ФинансовыеСтатьи.Добавить("002.0501", "Фонд ДП");
		ФинансовыеСтатьи.Добавить("002.0502", "Фонд КМ (реализация)");
	//ФинансовыеСтатьи.Добавить("002.0503", "Штрафы, пени, неустойки (по контрактам)");
	КонецЕсли;	
	
	Если РольДоступна(Метаданные.Роли.ПолныеПрава )
	или	 РольДоступна( Метаданные.Роли.КоммерческийДиректор ) Тогда
		// временно, для отладки
		//ФинансовыеСтатьи.Добавить("001.0101", "Законтрактованные");
		//ФинансовыеСтатьи.Добавить("001.0102", "Плановые");
		//ФинансовыеСтатьи.Добавить("001.0103", "Перспективные");
		ФинансовыеСтатьи.Добавить("001.0101", "По договорам");
		ФинансовыеСтатьи.Добавить("001.0102", "Гарантия");
		ФинансовыеСтатьи.Добавить("001.0103", "По задачам");
	КонецЕсли;
	
	ФинансовыеСтатьи.ЗаполнитьПометки( Истина );
	ФинансовыеСтатьи.СортироватьПоЗначению();
	
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	ЗаполнитьСписокСтатей();                              
КонецПроцедуры

&НаКлиенте
Процедура УстановитьФлажки(Команда)
	ФинансовыеСтатьи.ЗаполнитьПометки( Истина );
КонецПроцедуры

&НаКлиенте
Процедура СнятьФлажки(Команда)
	ФинансовыеСтатьи.ЗаполнитьПометки( Ложь );
КонецПроцедуры


#КонецОбласти