&НаСервереБезКонтекста
Функция ОтмеченныеЗадачиПроекта( ЗадачиПроекта )
	Стр = "";
	Для Каждого Эл ИЗ ЗадачиПроекта Цикл
		Если Эл.Пометка Тогда
			Стр = Стр + ?(ЗначениеЗаполнено(Стр), ", ", "") + Эл.Значение;
		КонецЕсли;
	КонецЦикла;
	Возврат Стр;
КонецФункции

&НаСервереБезКонтекста
Функция СуммаЗаТекущийМесяц( ТЗ, Статья, Месяц, ВариантОтчета, ФактВключаяМесяц)
	Отбор = Новый Структура;
	Отбор.Вставить("Статья", Статья);
	Отбор.Вставить("Месяц", Месяц );
	Строки = ТЗ.НайтиСтроки( Отбор );
	Если Строки.Количество() > 0 Тогда
		Если ВыводимСуммуПлана( Месяц, ВариантОтчета, ФактВключаяМесяц ) Тогда
			Возврат Строки[0].СуммаПлан;
		Иначе
			Возврат Строки[0].СуммаФакт;
		КонецЕсли;
		
		//Возврат Строки[0].СуммаЗаМесяц;
	КонецЕсли;
	Возврат 0; 
КонецФункции

&НаСервереБезКонтекста
Функция ВыводимСуммуПлана( Месяц,  ВариантОтчета, ФактВключаяМесяц )
	Если 		ВариантОтчета = 1 Тогда
		Возврат Истина;
	ИначеЕсли 	ВариантОтчета = 2 Тогда
		Возврат Ложь;
	Иначе
		Если Месяц <= ФактВключаяМесяц Тогда
			Возврат ВыводимСуммуПлана( Месяц, 2, ФактВключаяМесяц );
		Иначе
			Возврат ВыводимСуммуПлана( Месяц, 1, ФактВключаяМесяц );
		КонецЕсли;		
		
	КонецЕсли;
КонецФункции

&НаСервере
Процедура Сформировать_НаСервере( ТабДок )
	// 
    Макет 	= РеквизитФормыВЗначение("Отчет").ПолучитьМакет("ПланПроекта"); 

	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	БюджетПоМесяцамОбороты.СтатьяСметы КАК Статья,
		|	СУММА(БюджетПоМесяцамОбороты.СуммаОборот) КАК СуммаЗаМесяц,
		|	БюджетПоМесяцамОбороты.Месяц,
		|	СУММА(ВЫБОР
		|			КОГДА БюджетПоМесяцамОбороты.ТипСуммы = ЗНАЧЕНИЕ(Перечисление.ТипСуммыБюджета.Факт)
		|				ТОГДА БюджетПоМесяцамОбороты.СуммаОборот
		|			ИНАЧЕ 0
		|		КОНЕЦ) КАК СуммаФакт,
		|	СУММА(ВЫБОР
		|			КОГДА БюджетПоМесяцамОбороты.ТипСуммы = ЗНАЧЕНИЕ(Перечисление.ТипСуммыБюджета.План)
		|				ТОГДА БюджетПоМесяцамОбороты.СуммаОборот
		|			ИНАЧЕ 0
		|		КОНЕЦ) КАК СуммаПлан,
		|	ВЫБОР
		|		КОГДА БюджетПоМесяцамОбороты.Месяц <= &МесяцФакт
		|			ТОГДА ИСТИНА
		|		ИНАЧЕ ЛОЖЬ
		|	КОНЕЦ КАК ЭтотМесяцФакт
		|ИЗ
		|	РегистрНакопления.БюджетПоМесяцам.Обороты(, , , ЗадачаПроекта В (&Задачи)) КАК БюджетПоМесяцамОбороты
		|
		|СГРУППИРОВАТЬ ПО
		|	БюджетПоМесяцамОбороты.Месяц,
		|	БюджетПоМесяцамОбороты.СтатьяСметы";

		
	// с пометками
	мЗадач = Новый Массив;
	Для Каждого Эл ИЗ ЗадачиПроекта Цикл
		Если Эл.Пометка Тогда
			мЗадач.Добавить( Эл.Значение );
		КонецЕсли;
	КонецЦикла;
		
	Запрос.УстановитьПараметр("Задачи", 	мЗадач );
	Запрос.УстановитьПараметр("МесяцФакт", 	ФактВключаяМесяц );
	// рещультат
	Результат = Запрос.Выполнить().Выгрузить();
	
	Если Результат.Количество() <> 0 Тогда
		Результат.Сортировать("Месяц");
		ПервыйМесяц 	= Результат[0].Месяц;
		ПоследнийМесяц 	= Результат[Результат.Количество()-1].Месяц;
	Иначе
		Возврат;
	КонецЕсли;
	
	ОблШ = Макет.ПолучитьОбласть("Шапка|Начало");
	ОблШ.Параметры.ВидОтчета		= ?(ВариантОтчета=1, "План",
									  ?(ВариантОтчета=2, "Факт", "Факт, включая " + Формат( ФактВключаяМесяц, "ДФ='ММММ гггг'") + ", далее план"
									  ));
									  
	ОблШ.Параметры.Проект 			= Проект;
	ОблШ.Параметры.ЗадачиПроекта 	= ОтмеченныеЗадачиПроекта( ЗадачиПроекта );
	ТабДок.Вывести(ОблШ);
	
	ТекущийМесяц = ПервыйМесяц;
	Пока ТекущийМесяц <= ПоследнийМесяц Цикл
		ОблШД = Макет.ПолучитьОбласть("Шапка|ЗаМесяц");
		ОблШД.Параметры.Месяц 		= Формат( ТекущийМесяц, "ДФ = 'ММММ гггг'");
		ОблШД.Параметры.ТипСуммы 	= ?( ВыводимСуммуПлана( ТекущийМесяц, ВариантОтчета, ФактВключаяМесяц),  "план", "Факт");
		
		ТабДок.Присоединить( ОблШД );
		ТекущийМесяц = ДобавитьМесяц( ТекущийМесяц, 1);
		
	КонецЦикла;
	//
	//  готовим данные для отчета
	сМесяцы = Новый Соответствие;
	сВсего	= Новый Соответствие;
	
	// 2014
	СтруктураСметы 	= УП_СметаПроекта.ПолучитьСтруктуруСметыЗадачи( Проект.ГодПроекта );	
	
	ТекущийМесяц = ПервыйМесяц;
	Пока ТекущийМесяц <= ПоследнийМесяц Цикл
		сБюджетМесяца = Новый Соответствие;
		
		Для Каждого СтрСС ИЗ СтруктураСметы Цикл
			ИмяСтатьи 		= СтрСС.Ключ;
			Статья			= СтрСс.Значение.Статья;//.Статья;
			СуммаЗаМесяц	= СуммаЗаТекущийМесяц( Результат, Статья, ТекущийМесяц, ВариантОтчета, ФактВключаяМесяц );
			сБюджетМесяца.Вставить( ИмяСтатьи, СуммаЗаМесяц);
			
			// итого
			СуммаВсего = сВсего.Получить( ИмяСтатьи );
			сВсего.Вставить( ИмяСтатьи, СуммаЗаМесяц + ?(СуммаВсего=Неопределено,0,СуммаВсего));
			
		КонецЦикла;
		// пересчитываем итоговые статьи
		УП_СметаПроекта.ПересчетСметыЗадачи( СтруктураСметы, сБюджетМесяца );
		//
		сМесяцы.Вставить( ТекущийМесяц, сБюджетМесяца ); 
		//
		ТекущийМесяц = ДобавитьМесяц( ТекущийМесяц, 1);
	КонецЦикла;
	// пересчитываем итоговые статьи
	УП_СметаПроекта.ПересчетСметыЗадачи( СтруктураСметы, сВсего );
	
	 //!!!
	 //выводим отчет	
	 Для Каждого СтрСС ИЗ СтруктураСметы Цикл
		ИмяСтатьи 	= СтрСС.Ключ;
		ГорОбласть 	= ?(СтрСС.Значение.Итоговая, "СтатьяИт", "Статья" );
		
		ОблСтатья 						= Макет.ПолучитьОбласть(ГорОбласть + "|Начало");
		ОблСтатья.Параметры.КодСтатьи 	= СтрСС.Значение.КодСтатьи;
		ОблСтатья.Параметры.Статья 		= СтрСС.Значение.Статья; //???
		ОблСтатья.Параметры.Сумма 		= сВсего[ИмяСтатьи];
		ТабДок.Вывести(ОблСтатья);
		
		// по месяцам			
		ТекущийМесяц = ПервыйМесяц;
		Пока ТекущийМесяц <= ПоследнийМесяц Цикл
			ОблСтатьяД = Макет.ПолучитьОбласть( ГорОбласть + "|ЗаМесяц");
			ОблСтатьяД.Параметры.СуммаЗаМесяц = сМесяцы[ТекущийМесяц][ИмяСтатьи];
			ТабДок.Присоединить( ОблСтатьяД );
			ТекущийМесяц = ДобавитьМесяц( ТекущийМесяц, 1);
			
		КонецЦикла;
	КонецЦикла;
	
	
	//Если Проект.ГодПроекта < 2014 Тогда
	
		// выводим прибывль по проекту нарастающим итогом
		ГорОбласть						= "СтатьяИт";
		ОблСтатья 						= Макет.ПолучитьОбласть(ГорОбласть + "|Начало");
		
		
		Если Проект.ГодПроекта < 2014 Тогда
			ОблСтатья.Параметры.Статья 		= "Прибыль по проекту (нарастающим итогом)";
			ИмяСтатьи 						= "ПрибыльПоПроекту";
		Иначе
			ОблСтатья.Параметры.Статья 		= "Валовый доход по проекту (нарастающим итогом)";
			ИмяСтатьи 						= "ВаловыйДоход";
		КонецЕсли;
		//
		ОблСтатья.Параметры.Сумма 		= сВсего[ИмяСтатьи];
		ТабДок.Вывести(ОблСтатья);
		// по месяцам			
		ТекущийМесяц 			= ПервыйМесяц;
		СуммаНарастающимИтогом 	= 0;
		Пока ТекущийМесяц <= ПоследнийМесяц Цикл
			ОблСтатьяД 							= Макет.ПолучитьОбласть( ГорОбласть + "|ЗаМесяц");
			СуммаНарастающимИтогом				= СуммаНарастающимИтогом + сМесяцы[ТекущийМесяц][ИмяСтатьи];
			ОблСтатьяД.Параметры.СуммаЗаМесяц 	= СуммаНарастающимИтогом;
			ТабДок.Присоединить( ОблСтатьяД );
			ТекущийМесяц = ДобавитьМесяц( ТекущийМесяц, 1);
		КонецЦикла;
	//КонецЕсли;	
		
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьЗадачиПроекта()
	ЗадачиПроекта.Очистить();
	ВыбСпр = Справочники.ЗадачиПроектов.Выбрать( , Проект );
	Пока ВыбСпр.Следующий() Цикл
		ЗадачиПроекта.Добавить( ВыбСпр.Ссылка,, Истина );
	КонецЦикла;
КонецПроцедуры

&НаКлиенте
Процедура Сформировать(Команда)
	Если НЕ ЗначениеЗаполнено( ВариантОтчета ) Тогда
		Предупреждение("Укажите вариант отчета!",  10);
		Возврат;
	КонецЕсли;
	
	ТабДок = Новый ТабличныйДокумент;
	//
	Сформировать_НаСервере( ТабДок );
	
	//
	ТабДок.АвтоМасштаб			= Истина;
	ТабДок.ОриентацияСтраницы	= ОриентацияСтраницы.Ландшафт;
	ТабДок.ТолькоПросмотр		= Истина;
	ТабДок.Показать();
КонецПроцедуры

&НаКлиенте
Процедура ПроектПриИзменении(Элемент)
	ЗаполнитьЗадачиПроекта();
КонецПроцедуры


&НаКлиенте
Процедура УстановитьФлажки(Команда)
	ЗадачиПроекта.ЗаполнитьПометки( Истина );
КонецПроцедуры

&НаКлиенте
Процедура СнятьФлажки(Команда)
	ЗадачиПроекта.ЗаполнитьПометки( Ложь );
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
КонецПроцедуры

&НаКлиенте
Процедура ФактВключаяМесяцПриИзменении(Элемент)
	ФактВключаяМесяц = НачалоМесяца( ФактВключаяМесяц );
КонецПроцедуры
