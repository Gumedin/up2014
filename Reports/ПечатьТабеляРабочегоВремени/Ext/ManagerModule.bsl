Процедура Печать(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода) Экспорт
	
	// печать счёта на оплату
	ПодготовитьПечатнуюФорму(
		"ТабельРВ",
		НСтр("ru = 'Табель рабочего времени'"),
		"ОбщиеМакеты.ПФ_MXL_ТабельРВ",
		МассивОбъектов,
		КоллекцияПечатныхФорм,
		ОбъектыПечати,
		ПараметрыВывода,
		ПараметрыПечати);
	
		
	ПараметрыВывода.ДоступнаПечатьПоКомплектно = Ложь;
	
	
КонецПроцедуры


Процедура ПодготовитьПечатнуюФорму(Знач ИмяМакета, ПредставлениеМакета, ПолныйПутьКМакету = "", 
		  МассивОбъектов, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода, ПараметрыПечати)
	
	НужноПечататьМакет = УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, ИмяМакета);
	Если НужноПечататьМакет Тогда
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(
		КоллекцияПечатныхФорм,
		ИмяМакета,
		ПредставлениеМакета,
		ПечатьТабеляРабочегоВремени(МассивОбъектов, ОбъектыПечати, ИмяМакета, ПараметрыПечати),
		,
		ПолныйПутьКМакету);
	КонецЕсли;
	
КонецПроцедуры

// Процедура печати документа.
//
Функция ПечатьТабеляРабочегоВремени( МассивОбъектов, ОбъектыПечати, ИмяМакета, ПараметрыПечати) Экспорт
	
	
	ТабличныйДокумент = Новый ТабличныйДокумент;
	Запрос 			= Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ТРВРабочееВремя.Ссылка КАК Ссылка,
		|	ТРВРабочееВремя.НомерСтроки,
		|	ТРВРабочееВремя.ДатаТабеля,
		|	ТРВРабочееВремя.Начало,
		|	ТРВРабочееВремя.ОтметкаТабеля,
		|	ТРВРабочееВремя.КоличествоЧасов,
		|	ТРВРабочееВремя.СодержаниеРабот,
		|	ТРВРабочееВремя.Основание,
		|	ТРВРабочееВремя.ТарифнаяСтавка,
		|	ТРВРабочееВремя.Задача,
		|	ТРВРабочееВремя.Ссылка.ФизическоеЛицо
		|ИЗ
		|	Документ.ТабельУчетаРабочегоВремени.РабочееВремя КАК ТРВРабочееВремя
		|ГДЕ
		|	ТРВРабочееВремя.Ссылка.Проведен
		|	И ТРВРабочееВремя.ДатаТабеля МЕЖДУ &НачалоПериода И &КонецПериода";

	Если МассивОбъектов.Количество() <> 0 Тогда
		//Если ТипЗнч( МассивОбъектов[0] )= Тип("ДокументСсылка.ТабельУчетаРабочегоВремени") Тогда
		Если ПараметрыПечати.ТипПараметра = 1  Тогда
			
			Запрос.Текст = 	Запрос.Текст + "
				|
				|	И ТРВРабочееВремя.Ссылка.ФизическоеЛицо = &ФизическоеЛицо";
			
			Запрос.УстановитьПараметр("ФизическоеЛицо", ПараметрыПечати.ФизическоеЛицо);
			
			;
		Иначе
			Запрос.Текст = 	Запрос.Текст + "
				|
				|	И ТРВРабочееВремя.Основание В(&МассивОбъектов)";
			
			Запрос.УстановитьПараметр("МассивОбъектов", МассивОбъектов );
			
		КонецЕсли;
	Иначе
		Возврат ТабличныйДокумент;
	КонецЕсли;
	
	Запрос.УстановитьПараметр("НачалоПериода", 	ПараметрыПечати.Период.ДатаНачала);
	Запрос.УстановитьПараметр("КонецПериода", 	ПараметрыПечати.Период.ДатаОкончания);

	Результат = Запрос.Выполнить().Выгрузить();
	Результат.Сортировать( "ДатаТабеля,Начало");
	
	ТабличныйДокумент.КлючПараметровПечати = "ТабельРабочегоВремени";
	
	Макет 				= УправлениеПечатью.МакетПечатнойФормы("ОбщиеМакеты.ПФ_MXL_ТабельРВ");
	НомерСтрокиНачало 	= ТабличныйДокумент.ВысотаТаблицы + 1;
		
	СформироватьТабельРабочегоВремени(ТабличныйДокумент, Результат, Макет, ПараметрыПечати, МассивОбъектов[0] );
		
	// конец вывода документа
	// сслку надо поменять на МассивОбъектов[0].Ссылка - проверить если она есть
	УправлениеПечатью.ЗадатьОбластьПечатиДокумента(ТабличныйДокумент, НомерСтрокиНачало, ОбъектыПечати, МассивОбъектов[0].Ссылка);
	
	
	Возврат ТабличныйДокумент;
	
КонецФункции





//***************************************************************************************
// норма часов
Процедура ВывестиОтметкиЗаДень( ТабДок, МакетТаб, ТипПараметра, Результат, Отметки, ДатаТабеля, НормаЧасов, СверхНормы, РабочихЧасов  )
	//
	ОтметкиЗаДень 	= Результат.Скопировать( Новый Структура("ДатаТабеля", ДатаТабеля));
	РабочихЧасов		= 0;
	Для Каждого Отметка ИЗ ОтметкиЗаДень Цикл
		Если Отметка.ОтметкаТабеля.РабочееВремя Тогда
			РабочихЧасов = РабочихЧасов + Отметка.КоличествоЧасов;
		КонецЕсли;
	КонецЦикла;
	//
	СверхНормы = МАКС(РабочихЧасов - НормаЧасов, 0);
		
	ТипДня			= ?(НормаЧасов=0,"В", "Р");
	ТипСтроки		= "СтрокаСДатой";
	
	// построчно выводим все дни, даже если нет отметок
	Н = 0;
	Пока Н <= ОтметкиЗаДень.Количество() Цикл
		// начало строки
		ОбластьОтм 		= МакетТаб.ПолучитьОбласть( ТипДня + ТипСтроки + "|Начало");
		Если ТипСтроки		= "СтрокаСДатой" Тогда
			ОбластьОтм.Параметры.ТекДата 			= ДатаТабеля;
			Если ТипПараметра = 1 Тогда
				ОбластьОтм.Параметры.ПечНорма			= НормаЧасов;
				ОбластьОтм.Параметры.ПечСверхНормативно	= СверхНормы;
			КонецЕсли;
			
		КонецЕсли;
		
		Если Н < ОтметкиЗаДень.Количество() Тогда // есть данные
			ОбластьОтм.Параметры.Начало = ОтметкиЗаДень[Н].Начало;
		КонецЕсли;
				
		ТабДок.Вывести(ОбластьОтм);
		
		// выводим отметки		
		ОбластьОтм = МакетТаб.ПолучитьОбласть( ТипДня + ТипСтроки+ "|НачалоОтм");
		ТабДок.Присоединить(ОбластьОтм);
		
		Для Каждого СтрО ИЗ Отметки Цикл
			ОбластьОтм = МакетТаб.ПолучитьОбласть(ТипДня + ТипСтроки+ "|Отметка");
			Если Н < ОтметкиЗаДень.Количество() Тогда // есть данные
				Если ОтметкиЗаДень[Н].ОтметкаТабеля = СтрО.ОтметкаТабеля Тогда
					ОбластьОтм.Параметры.Часы = ОтметкиЗаДень[Н].КоличествоЧасов;
				КонецЕсли;
			КонецЕсли;
			ТабДок.Присоединить(ОбластьОтм);
		КонецЦикла;
		
		ОбластьОтм = МакетТаб.ПолучитьОбласть( ТипДня + ТипСтроки+ "|Конец");
		// конец
		Если Н < ОтметкиЗаДень.Количество() Тогда // есть данные
			Если ТипПараметра = 1 Тогда
				ОбластьОтм.Параметры.Проект			= ОтметкиЗаДень[Н].Основание;
			Иначе
				ОбластьОтм.Параметры.Проект			= ОтметкиЗаДень[Н].ФизическоеЛицо;
			КонецЕсли;
			ОбластьОтм.Параметры.СодержаниеРабот	= ОтметкиЗаДень[Н].СодержаниеРабот;
		КонецЕсли;
		ТабДок.Присоединить(ОбластьОтм);
		
		// следующие - без даты
		ТипСтроки	= "СтрокаБезДаты";
		Н 			= Н + 1;
		Если Н = ОтметкиЗаДень.Количество() Тогда
			Прервать;
		КонецЕсли;
		
	КонецЦикла;
	
		
КонецПроцедуры


Процедура СформироватьТабельРабочегоВремени( ТабДок, Результат, МакетТаб, ПараметрыПечати, Основание )
	Отметки   = Результат.Скопировать(,"ОтметкаТабеля");
	Отметки.Свернуть("ОтметкаТабеля", "");
	
	// начинаем вывод шапки табеля
	ОбластьШ = МакетТаб.ПолучитьОбласть("Шапка|Начало");
	ПечОснование = "" + Основание;
	Если ТипЗнч( Основание ) = Тип("ДокументСсылка.ТабельУчетаРабочегоВремени") Тогда
		ПечОснование = ПечОснование + ", " + Основание.ФизическоеЛицо;
	КонецЕсли;
	ОбластьШ.Параметры.Основание 		= ПечОснование;
	ОбластьШ.Параметры.ПечПериодТабеля 	= ПредставлениеПериода( ПараметрыПечати.Период.ДатаНачала, ПараметрыПечати.Период.ДатаОкончания, "ФП=Истина");
	ТабДок.Вывести(ОбластьШ);
	ОбластьШ = МакетТаб.ПолучитьОбласть("Шапка|НачалоОтм");
	ТабДок.Присоединить(ОбластьШ);
	
	Для Каждого СтрО ИЗ Отметки Цикл
		ОбластьШ = МакетТаб.ПолучитьОбласть("Шапка|Отметка");
		ОбластьШ.Параметры.ОтметкаТабеля = СтрО.ОтметкаТабеля;
		ТабДок.Присоединить(ОбластьШ);
	КонецЦикла;
	ОбластьШ = МакетТаб.ПолучитьОбласть("Шапка|Конец");
	ОбластьШ.Параметры.СотрудникПроект	=  ?(ПараметрыПечати.ТипПараметра=1, "Основание", "ФизическоеЛицо");
	ТабДок.Присоединить(ОбластьШ);
	
	// все дни по календарю, независимо от наличия отметок
	ИтСверхНормы		= 0;
	ИтНорма				= 0;
	ИтРабочихЧасов		= 0;
	Сутки				= 24*60*60;
	
	
	ДатаТабеля 			= ПараметрыПечати.Период.ДатаНачала;
	Пока ДатаТабеля <= ПараметрыПечати.Период.ДатаОкончания Цикл
		// 
		НормаЧасов	= УП_ПланыРаботПоПроектам.КоличествоРабочихЧасовВДне( ДатаТабеля );
		РабочихЧасов= 0;
		СверхНормы	= 0;
		
		ВывестиОтметкиЗаДень( ТабДок, МакетТаб, ПараметрыПечати.ТипПараметра, Результат, Отметки, ДатаТабеля, НормаЧасов, СверхНормы, РабочихЧасов );
		//			
		ИтСверхНормы 		= ИтСверхНормы		+ СверхНормы;
		ИтНорма				= ИтНорма 			+ НормаЧасов;
		ИтРабочихЧасов		= ИтРабочихЧасов 	+ РабочихЧасов;
		//
		ДатаТабеля = ДатаТабеля + Сутки;
	КонецЦикла;
	
	// подвал
	ОбластьИ = МакетТаб.ПолучитьОбласть("Итого|Начало");
	Если ПараметрыПечати.ТипПараметра = 1 Тогда
		ОбластьИ.Параметры.ИтНорма 		= ИтНорма;
		ОбластьИ.Параметры.ИтСверхНормы	= ИтСверхНормы;
	КонецЕсли;
	ТабДок.Вывести(ОбластьИ);
	
	Результат.Свернуть("ОтметкаТабеля", "КоличествоЧасов");
	ОбластьИ = МакетТаб.ПолучитьОбласть("Итого|НачалоОтм");
	ТабДок.Присоединить(ОбластьИ);
	ИтРабочегоВремени 	= 0;
	ИтНеРабочегоВремени	= 0;
	
	Для Каждого СтрО ИЗ Отметки Цикл
		ОбластьИ 	= МакетТаб.ПолучитьОбласть("Итого|Отметка");
		СтрРез 		= Результат.Найти( СтрО.ОтметкаТабеля, "ОтметкаТабеля");
		Если СтрРез <> Неопределено Тогда
			Если СтрО.ОтметкаТабеля.РабочееВремя Тогда
				ОбластьИ.Параметры.ИтПоРВОтметке	= СтрРез.КоличествоЧасов;
				ИтРабочегоВремени					= ИтРабочегоВремени + СтрРез.КоличествоЧасов;
			Иначе
				ОбластьИ.Параметры.ИтПоНеРВОтметке 	= СтрРез.КоличествоЧасов;
				ИтНеРабочегоВремени 				= ИтНеРабочегоВремени + СтрРез.КоличествоЧасов;
			КонецЕсли;
		КонецЕсли;
		ТабДок.Присоединить(ОбластьИ);
	КонецЦикла;
	ОбластьИ = МакетТаб.ПолучитьОбласть("Итого|Конец");
	ОбластьИ.Параметры.ИтРабочегоВремени 	= ИтРабочегоВремени;
	Если ПараметрыПечати.ТипПараметра = 1 Тогда
		ОбластьИ.Параметры.ИтНеРабочегоВремени 	= ИтНеРабочегоВремени;
	КонецЕсли;
	ТабДок.Присоединить(ОбластьИ);
	
	
КонецПроцедуры

