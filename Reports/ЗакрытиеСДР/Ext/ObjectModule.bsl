Функция ПолучитьПериодРесурсаПоМесяцам(Ресурс, Год )
	ПериодРесурса = Новый СтандартныйПериод;
	Если Ресурс.Работа.ТипРасчетаСрокаРаботы = Перечисления.ТипыРасчетаСрокаРабот.ПоЗадачеПроекта Тогда
		ПериодРесурса.ДатаНачала 	= Ресурс.НачалоЗадачи;
		ПериодРесурса.ДатаОкончания = Ресурс.ОкончаниеЗадачи;
		Возврат ПериодРесурса;
	КонецЕсли;
	
	
	// иначе берем из работы
	Если 		ЗначениеЗаполнено( Ресурс.НачалоРабот ) Тогда
		ПериодРесурса.ДатаНачала = Ресурс.НачалоРабот;
	Иначе
		ПериодРесурса.ДатаНачала = НачалоГода(Дата( Год, 1, 1));
	КонецЕсли;
		
	Если 		ЗначениеЗаполнено( Ресурс.ОкончаниеРабот ) Тогда
		ПериодРесурса.ДатаОкончания = Ресурс.ОкончаниеРабот;
	Иначе
		ПериодРесурса.ДатаОкончания = КонецГода( Дата( Год, 1, 1));
	КонецЕсли;
	Возврат ПериодРесурса;
КонецФункции


// разбиваем таблицу по месяцам
Процедура РаспределитьРесурсПоМесяцам( СДР, ПериодРесурса, Ресурс )
	// распределение рабочих дней по месяцам
	сРД = УП_ПланыРаботПоПроектам.РабочиеДниПоМесяцам( ПериодРесурса.ДатаНачала, ПериодРесурса.ДатаОкончания);
	
	
	// формируем таблицу коэффициентов распределения (пропорционально рабочим дням)
	// количества часов по месяцам ресурса
	мКоэфф  = Новый Массив;
	Для Каждого ЗаМесяц ИЗ сРД Цикл
		мКоэфф.Добавить( ЗаМесяц.Значение.рДней );
	КонецЦикла;
	// распределяем количество ресурса по месяцам
	мКоличество = РаспределитьПропорционально( Ресурс.КоличествоСДР, мКоэфф, 0 );
	Если мКоличество= Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ИндексМесяца = 0;
	Для Каждого ЗаМесяц ИЗ сРД Цикл
		ТекущийМесяц 	= НачалоМесяца( ЗаМесяц.Ключ );
		СтрСДР 			= СДР.Добавить();
		// стандартные
		ЗаполнитьЗначенияСвойств( СтрСДР, Ресурс );
		// если должность не заполнена, а тарифная ставка указана, пытаемся по тарифной ставке
		Если НЕ ЗначениеЗаполнено( СтрСДР.Должность ) Тогда
			СтрСДР.Должность = 
			УП_КадрыСервер.ДолжностьСотрудникаПоТарифнойСтавке( Ресурс.ТарифнаяСтавка, 
																Ресурс.ФизическоеЛицо, 
																Ресурс.ЗадачаПроекта.Подразделение, 
																ТекущийМесяц);
		КонецЕсли;
		//
		СтрСДР.Месяц 			= ТекущийМесяц;
		СтрСДР.КоличествоСДР 	= мКоличество[ИндексМесяца];
		СтавкаФОТ				= СтавкаФОТПодразделения( 	Ресурс.ЗадачаПроекта.Подразделение, Ресурс.ТарифнаяСтавка, ТекущийМесяц);  
		// сумма в часах
		СтрСДР.СуммаСДР 		= СтрСДР.КоличествоСДР * СтавкаФОТ;
		ИндексМесяца			= ИндексМесяца + 1;
		
		// заполним в другом месте
		СтрСДР.КоличествоЗакрыто 	= 0;
		СтрСДР.СуммаЗакрыто		= 0;
	КонецЦикла;
КонецПроцедуры

// закрытие планов работ в Табелях ЗП
Процедура ОтразитьЗакрытыеРаботы(мПроектов, ГодПроекта, СДР)
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ТРЗП.Ссылка.ПланРабот.ЗадачаПроекта КАК ЗадачаПроекта,
		|	ТРЗП.Задача КАК Работа,
		|	ТРЗП.ТарифнаяСтавка,
		|	ТРЗП.Должность,
		|	ТРЗП.ФизическоеЛицо,
		|	ТРЗП.Ссылка.ПериодРегистрации КАК Месяц,
		|	СУММА(ТРЗП.Количество) КАК КоличествоЗакрыто,
		|	СУММА(0) КАК СуммаЗакрыто,
		|	СУММА(0) КАК Количество,
		|	СУММА(0) КАК Сумма,
		|	ТРЗП.Подразделение,
		|	ТРЗП.Ссылка.ПланРабот.ЗадачаПроекта.Владелец КАК Проект
		|ИЗ
		|	Документ.ТабельРаботПоЗадачеПроекта.РабочееВремя КАК ТРЗП
		|ГДЕ
		|	ТРЗП.Ссылка.ПланРабот.ЗадачаПроекта.Владелец В(&мПроектов)
		|	И ТРЗП.Ссылка.Проведен
		|	И ТРЗП.Ссылка.ПланРабот.ЗадачаПроекта.Владелец.ГодПроекта = &ГодПроекта
		|
		|СГРУППИРОВАТЬ ПО
		|	ТРЗП.ТарифнаяСтавка,
		|	ТРЗП.Ссылка.ПланРабот.ЗадачаПроекта,
		|	ТРЗП.Должность,
		|	ТРЗП.Ссылка.ПериодРегистрации,
		|	ТРЗП.Задача,
		|	ТРЗП.ФизическоеЛицо,
		|	ТРЗП.Подразделение";
		
	Запрос.УстановитьПараметр("мПроектов", 	мПроектов);
	Запрос.УстановитьПараметр("ГодПроекта", ГодПроекта);
	
	РезультатЗапроса = Запрос.Выполнить().Выгрузить();
	Для Каждого СтрРЗ ИЗ РезультатЗапроса Цикл
		СтавкаФОТ			= СтавкаФОТПодразделения( 	СтрРЗ.Подразделение, СтрРЗ.ТарифнаяСтавка, СтрРЗ.Месяц);  
		//
		СтрСДР = СДР.Добавить();
		ЗаполнитьЗначенияСвойств( СтрСДР, СтрРЗ );
		СтрСДР.СуммаЗакрыто = СтрСДР.КоличествоЗакрыто * СтавкаФОТ;
	КонецЦикла;
	
	СДР.Свернуть( "Проект,ЗадачаПроекта,Работа,ТарифнаяСтавка,Должность,ФизическоеЛицо,Месяц", "КоличествоСДР,СуммаСДР,КоличествоЗакрыто,СуммаЗакрыто");
	
	
КонецПроцедуры


// в разрезе Должность, ФизЛицо, ТарифнаяСтавка 
Функция ПолучитьСДРПоМесяцам( мПроектов, ГодПроекта )
	

	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	РесурсыРабот.Ссылка.РаннийСтарт КАК НачалоРабот,
		|	РесурсыРабот.Ссылка.ПозднийФиниш КАК ОкончаниеРабот,
		|	РесурсыРабот.Ссылка.Владелец.НачалоРабот КАК НачалоЗадачи,
		|	РесурсыРабот.Ссылка.Владелец.ОкончаниеРабот КАК ОкончаниеЗадачи,
		|	СУММА(РесурсыРабот.Количество * РесурсыРабот.Мощность) КАК КоличествоСДР,
		|	РесурсыРабот.ТарифнаяСтавка,
		|	ВЫБОР
		|		КОГДА ТИПЗНАЧЕНИЯ(РесурсыРабот.Исполнитель) = ТИП(Справочник.ФизическиеЛица)
		|			ТОГДА РесурсыРабот.Исполнитель
		|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.ФизическиеЛица.ПустаяСсылка)
		|	КОНЕЦ КАК ФизическоеЛицо,
		|	ВЫБОР
		|		КОГДА ТИПЗНАЧЕНИЯ(РесурсыРабот.Исполнитель) = ТИП(Справочник.Должности)
		|			ТОГДА РесурсыРабот.Исполнитель
		|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.Должности.ПустаяСсылка)
		|	КОНЕЦ КАК Должность,
		|	РесурсыРабот.Ссылка.Владелец КАК ЗадачаПроекта,
		|	РесурсыРабот.Ссылка КАК Работа,
		|	РесурсыРабот.Ссылка.Владелец.Владелец КАК Проект
		|ИЗ
		|	Справочник.ЗадачиПроектовСтруктура.Ресурсы КАК РесурсыРабот
		|ГДЕ
		|	РесурсыРабот.Ссылка.Владелец.Владелец В(&мПроектов)
		|	И НЕ РесурсыРабот.Ссылка.Владелец.ПометкаУдаления
		|	И НЕ РесурсыРабот.Ссылка.ПометкаУдаления
		|	И РесурсыРабот.Ссылка.Владелец.Владелец.ГодПроекта = &ГодПроекта
		|
		|СГРУППИРОВАТЬ ПО
		|	ВЫБОР
		|		КОГДА ТИПЗНАЧЕНИЯ(РесурсыРабот.Исполнитель) = ТИП(Справочник.ФизическиеЛица)
		|			ТОГДА РесурсыРабот.Исполнитель
		|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.ФизическиеЛица.ПустаяСсылка)
		|	КОНЕЦ,
		|	РесурсыРабот.ТарифнаяСтавка,
		|	РесурсыРабот.Ссылка.РаннийСтарт,
		|	РесурсыРабот.Ссылка.ПозднийФиниш,
		|	РесурсыРабот.Ссылка.Владелец.НачалоРабот,
		|	РесурсыРабот.Ссылка.Владелец.ОкончаниеРабот,
		|	РесурсыРабот.Ссылка.Владелец,
		|	РесурсыРабот.Ссылка,
		|	ВЫБОР
		|		КОГДА ТИПЗНАЧЕНИЯ(РесурсыРабот.Исполнитель) = ТИП(Справочник.Должности)
		|			ТОГДА РесурсыРабот.Исполнитель
		|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.Должности.ПустаяСсылка)
		|	КОНЕЦ,
		|	РесурсыРабот.Ссылка.Владелец.Владелец";
		
		
	// по проекту
	Запрос.УстановитьПараметр("мПроектов", 	мПроектов);
	Запрос.УстановитьПараметр("ГодПроекта", ГодПроекта);
	
	РезультатЗапроса = Запрос.Выполнить().Выгрузить();
	//РезультатЗапроса.Сортировать("Проект,ЗадачаПроекта,Работа");
	
	// 
	СДР = Новый ТаблицаЗначений;
	СДР.Колонки.Добавить("Проект");
	СДР.Колонки.Добавить("ЗадачаПроекта");
	СДР.Колонки.Добавить("Работа");
	СДР.Колонки.Добавить("ТарифнаяСтавка"); 
	СДР.Колонки.Добавить("Должность"); 
	СДР.Колонки.Добавить("ФизическоеЛицо"); 
	СДР.Колонки.Добавить("Месяц"); 			// месяц работ
	СДР.Колонки.Добавить("КоличествоСДР", 	Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(5, 0))); 
	СДР.Колонки.Добавить("СуммаСДР", 		Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(15, 2)));
	
	// закрыто
	СДР.Колонки.Добавить("КоличествоЗакрыто",  	Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(5, 0)));
	СДР.Колонки.Добавить("СуммаЗакрыто",  		Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(15, 2)));
	
	// добавляем разрез по месяцам
	Для Каждого Ресурс ИЗ РезультатЗапроса Цикл
		// период распределения ресурса
		ПериодРесурса = ПолучитьПериодРесурсапоМесяцам(Ресурс, ГодПроекта);
		РаспределитьРесурсПоМесяцам( СДР, ПериодРесурса, Ресурс );
	КонецЦикла;
	
	// показываем закрыто
	ОтразитьЗакрытыеРаботы( мПроектов, ГодПроекта,  СДР);
	//СДР.Сортировать("Проект,ЗадачаПроекта,Работа");
	
	Возврат СДР;
	
КонецФункции


Процедура ПриКомпоновкеРезультата(ДокументРезультат, ДанныеРасшифровки, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	//
	НастройкиСКД		= КомпоновщикНастроек.ПолучитьНастройки();
	
	//Получение самой СКД из макета
	СхемаКомпоновкиДанных = ПолучитьМакет("ОсновнаяСхемаКомпоновкиДанных");
	//
	
	
	//Макет компоновки
	КомпоновщикМакета = Новый КомпоновщикМакетаКомпоновкиДанных;
	МакетКомпоновки = КомпоновщикМакета.Выполнить(СхемаКомпоновкиДанных,НастройкиСКД, ДанныеРасшифровки );
	
	// параметры из СКД
	мПроектов 	= СКД_МассивРазрешенныхПроектов();
	ГодПроекта	= МакетКомпоновки.ЗначенияПараметров.ГодПроекта.Значение;
	
	
	//Связь между таблицей значений и именами в СКД
	ВнешниеНаборыДанных = Новый Структура;
	тзСДР =  ПолучитьСДРПоМесяцам( мПроектов, ГодПроекта );
	ВнешниеНаборыДанных.Вставить("тзСДР", тзСДР);
	
	//Компоновка данных
	ПроцессорКомпоновки = Новый ПроцессорКомпоновкиДанных;
	ПроцессорКомпоновки.Инициализировать(МакетКомпоновки,ВнешниеНаборыДанных,ДанныеРасшифровки);

	//Вывод результата
	ДокументРезультат.Очистить(); 
	ПроцессорВывода = Новый ПроцессорВыводаРезультатаКомпоновкиДанныхВТабличныйДокумент;
	ПроцессорВывода.УстановитьДокумент(ДокументРезультат);
	ПроцессорВывода.Вывести(ПроцессорКомпоновки);
	//ДокументРезультат.Показать();

	//ДокументРезультат = Новый ТабличныйДокумент;
	ДокументРезультат.АвтоМасштаб=Истина;
	ДокументРезультат.ОриентацияСтраницы=ОриентацияСтраницы.Ландшафт;
	
КонецПроцедуры

