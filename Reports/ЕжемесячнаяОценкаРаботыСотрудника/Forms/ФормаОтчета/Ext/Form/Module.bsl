//**********************************************************************************
//  В одном месяце по одному критерию берется максимальная оценка (т.е. самая плохая)
&НаСервереБезКонтекста
Функция ОценкаСотрудниковЗаМесяц( ФизическиеЛица, МесяцОценки ) 
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ОценкиСотрудников.Оценка КАК Оценка,
		|	ОценкиСотрудников.Период КАК МесяцОценки,
		|	ОценкиСотрудников.КритерийОценки,
		|	ОценкиСотрудников.Оценщик,
		|	ОценкиСотрудников.ФизическоеЛицо,
		|	ОценкиСотрудников.Проект,
		|	ОценкиСотрудников.Комментарий
		|ИЗ
		|	РегистрНакопления.ОценкиСотрудников КАК ОценкиСотрудников
		|ГДЕ
		|	ОценкиСотрудников.Период <= &МесяцОценки
		|	И ОценкиСотрудников.ФизическоеЛицо В(&ФизическиеЛица)";

	Запрос.УстановитьПараметр("МесяцОценки", МесяцОценки);
	Запрос.УстановитьПараметр("ФизическиеЛица",  ФизическиеЛица);
	
	Результат	= Запрос.Выполнить().Выгрузить();
	
	// группируем по оценщику
	тзСвод		= Результат.Скопировать();
	тзСвод.Свернуть("Оценщик,ФизическоеЛицо,КритерийОценки,МесяцОценки", "Оценка");
	
	// суммируем комментарии одного оценщика
	тзСвод.Колонки.Добавить("Комментарий",  Новый ОписаниеТипов("Строка", Новый КвалификаторыСтроки(0)));
	Для Каждого СтрР из Результат Цикл
		Если ПустаяСтрока( СтрР.Комментарий ) Тогда Продолжить; КонецЕсли;
		
		Отбор = Новый Структура;
		Отбор.Вставить("ФизическоеЛицо", 	СтрР.ФизическоеЛицо);
		Отбор.Вставить("Оценщик", 	 		СтрР.Оценщик);
		Отбор.Вставить("КритерийОценки", 	СтрР.КритерийОценки);
		Отбор.Вставить("МесяцОценки", 	 	СтрР.МесяцОценки);
		// 
		ВыбСтроки =  тзСвод.НайтиСтроки( Отбор );
		Для Каждого СтрВ ИЗ ВыбСтроки Цикл
			СтрВ.Комментарий = "" + СтрР.Оценка + "-" + СокрЛП(СтрВ.Комментарий ) + " " +  СокрЛП( СтрР.Комментарий);// + Символы.ПС;
		КонецЦикла;
	КонецЦикла;
	
	тз = Новый ТаблицаЗначений;
	тз.Колонки.Добавить("ФизическоеЛицо");
	тз.Колонки.Добавить("МесяцОценки");
	тз.Колонки.Добавить("КритерийОценки");
	тз.Колонки.Добавить("Оценка");
	тз.Колонки.Добавить("Комментарий");
	
	Для Каждого СтрР из тзСвод Цикл
		Отбор = Новый Структура;
		Отбор.Вставить("ФизическоеЛицо",	СтрР.ФизическоеЛицо);
		Отбор.Вставить("КритерийОценки", 	СтрР.КритерийОценки);
		Отбор.Вставить("МесяцОценки", 	 	СтрР.МесяцОценки);
		// 
		ВыбСтроки =  тз.НайтиСтроки( Отбор );
		Если ВыбСтроки.Количество() = 0 Тогда
			ТекСтр = тз.Добавить();
			ТекСтр.ФизическоеЛицо 	= СтрР.ФизическоеЛицо;
			ТекСтр.МесяцОценки	 	= СтрР.МесяцОценки;
			ТекСтр.КритерийОценки	= СтрР.КритерийОценки;
			ТекСтр.Оценка			= СтрР.Оценка;
			ТекСтр.Комментарий		= СтрР.Комментарий;
		Иначе
			ТекСтр = ВыбСтроки[0];
		КонецЕсли;
		//
		Если СтрР.Оценка > ТекСтр.Оценка Тогда
		// победил
			ТекСтр.Оценка 		= Макс( ТекСтр.Оценка, СтрР.Оценка);
			ТекСтр.Комментарий	= СтрР.Комментарий;
		КонецЕсли;
	КонецЦикла;
	
	
	Возврат тз;
	
КонецФункции


&НаСервере
Функция ТаблицаДляПечати( тзРез, МесяцОценки )
	
	// Р - !!! ОСТАТОК !!! коэффициента
	тзПеч = Новый ТаблицаЗначений;
	тзПеч.Колонки.Добавить("ФизическоеЛицо");
	тзПеч.Колонки.Добавить("ВходОстаток", 	Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(4, 1, ДопустимыйЗнак.Неотрицательный)));
	тзПеч.Колонки.Добавить("Оценка", 		Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(4, 1, ДопустимыйЗнак.Неотрицательный)));
	тзПеч.Колонки.Добавить("ОценкаИтого", 	Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(4, 1, ДопустимыйЗнак.Неотрицательный)));
	тзПеч.Колонки.Добавить("ОценкаР", 		Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(4, 1, ДопустимыйЗнак.Неотрицательный)));
	тзПеч.Колонки.Добавить("Перенос", 		Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(4, 1, ДопустимыйЗнак.Неотрицательный)));
	тзПеч.Колонки.Добавить("Комментарий");
	// для расчета количества месяцев (т.е. максимального количества Р)
	тзПеч.Колонки.Добавить("КолМесяцев", 	Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(3, 0, ДопустимыйЗнак.Неотрицательный)));
	
	
	// т.к. непонятно, как сворачивать текстовый комментарий, сделаем копию таблицы,
	// из исходной потом возьмем комментарий
	тзР = тзРез.Скопировать();
	// убираем критерий оценки	
	тзР.Свернуть("ФизическоеЛицо,МесяцОценки", "Оценка");
	
	// отрабатываем месяцы
	Для Каждого СтрОц  ИЗ тзР Цикл
		СтрПеч	= тзПеч.Найти(СтрОц.ФизическоеЛицо, "ФизическоеЛицо");
		Если СтрПеч = Неопределено Тогда
			СтрПеч 					= тзПеч.Добавить();
			СтрПеч.ФизическоеЛицо 	= СтрОц.ФизическоеЛицо;
			СтрПеч.Комментарий  	= "";
		КонецЕсли;
		
		// если за какой- то месяц
		Если СтрОц.МесяцОценки < МесяцОценки Тогда
			СтрПеч.ВходОстаток = СтрПеч.ВходОстаток + СтрОц.Оценка;
			СтрПеч.КолМесяцев  = СтрПеч.КолМесяцев  + 1;
		Иначе
			СтрПеч.Оценка 		= СтрОц.Оценка;
			
			// комментарий выводим только для оценок, полученных за текущий месяц
			Отбор = Новый Структура;
			Отбор.Вставить("МесяцОценки", 		МесяцОценки);
			Отбор.Вставить("ФизическоеЛицо", 	СтрОц.ФизическоеЛицо);
			СтрТзРез = тзРез.НайтиСтроки( Отбор );
			Если СтрТзРез.Количество() > 0 Тогда
				Для Каждого Стр ИЗ СтрТзРез Цикл
					СтрПеч.Комментарий = СтрПеч.Комментарий + 
										 ?(ПустаяСтрока(СтрПеч.Комментарий),"", Символы.ПС)+
										 Стр.Комментарий;
				КонецЦикла;
			КонецЕсли;
			
		КонецЕсли;
	КонецЦикла;
	
	// последний этап - переходящие остатки
	Для Каждого СтрПеч ИЗ тзПеч Цикл
		// если входящий остаток больше количества месяцев оценок (т.к. каждый месяц списывается 1)
		СтрПеч.ВходОстаток 		= ?(СтрПеч.ВходОстаток > СтрПеч.КолМесяцев, СтрПеч.ВходОстаток - СтрПеч.КолМесяцев, 0);
		// не больше 1
		СтрПеч.ОценкаИтого		= МИН( 1, СтрПеч.ВходОстаток + СтрПеч.Оценка);
		// остальное на следующие месяцы
		СтрПеч.Перенос			= СтрПеч.ВходОстаток + СтрПеч.Оценка - СтрПеч.ОценкаИтого;
		СтрПеч.ОценкаР			= 1 - СтрПеч.ОценкаИтого;
		
		
	КонецЦикла;
	
	Возврат тзПеч;
	
КонецФункции

&НаСервере
Процедура СформироватьОтчетНаСервере( ТабДок )
	
	СпФизЛиц = Новый СписокЗначений;
	Для Каждого Сотр ИЗ Сотрудники Цикл
		Если Сотр.Флажок Тогда
			СпФизЛиц.Добавить(Сотр.ФизическоеЛицо);
		КонецЕсли;
	КонецЦикла;
	Если СпФизЛиц.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	// оценки сведенные по всем оценщикам и проектам
	// не сведенные по месяцам и критериям оценки
	тзР = ОценкаСотрудниковЗаМесяц( СпФизЛиц, МесяцОценки );
	
	// приводим к виду, удобному для печати
	тзПеч = ТаблицаДляПечати( тзР, МесяцОценки );
	
	Макет  		= Отчеты.ЕжемесячнаяОценкаРаботыСотрудника.ПолучитьМакет("Макет");
	Область		= Макет.ПолучитьОбласть("Шапка");
	Область.Параметры.ПечПодразделение 	= Подразделение;
	Область.Параметры.ПечМесяц 			= ПредставлениеПериода( НачалоМесяца(МесяцОценки), КонецМесяца( МесяцОценки )); 
	
	
	ТабДок.Вывести(Область);
	НомерСотр = 0;
	
	Для Каждого Сотр Из Сотрудники Цикл
		Если Не Сотр.Флажок Тогда Продолжить; КонецЕсли;
		НомерСотр = НомерСотр + 1;
		
		Область = Макет.ПолучитьОбласть("ОценкаСотрудника");
		Область.Параметры.НомерСотр			= НомерСотр;
		Область.Параметры.ФизическоеЛицо 	= Сотр.ФизическоеЛицо;
		// для тех, у кого не было ничего
		Область.Параметры.ОценкаР		= 1;
		
		СтрОц = тзПеч.Найти( Сотр.ФизическоеЛицо, "ФизическоеЛицо");
		Если СтрОц = Неопределено Тогда
		Иначе
			Область.Параметры.Заполнить( СтрОц );
		КонецЕсли;
		
		ТабДок.Вывести(Область);
	КонецЦикла;
	
	Область = Макет.ПолучитьОбласть("Подвал");
	Рук 	= УП_КадрыСервер.РуководительПодразделения( Подразделение, КонецМесяца(МесяцОценки));
	Область.Параметры.ПечРуководитель 	= Рук.Руководитель;
	Область.Параметры.ПечДолжность		= Рук.Должность;
	Область.Параметры.ПечДата 			= Формат( ТекущаяДата(), "ДЛФ=Д");
	ТабДок.Вывести(Область);
	
	
	
КонецПроцедуры

&НаКлиенте
Процедура Печать(Команда)
	ПустаяДата = '00010101';
	
	// Вставить содержимое обработчика.
	Если МесяцОценки = ПустаяДата Тогда
		Предупреждение("Не указан месяц!");
		Возврат;
	КонецЕсли;
	
	ТабДок = Новый ТабличныйДокумент;
	
	СформироватьОтчетНаСервере( ТабДок );
	
	//ТабДок.Защита = Истина;
	ТабДок.АвтоМасштаб 			= Истина;
	ТабДок.ОриентацияСтраницы	= ОриентацияСтраницы.Портрет;
	ТабДок.Показать();
	
	
КонецПроцедуры

//****
&НаСервере
Процедура ЗаполнитьСписокСотрудниковНаСервере()
	
	Период 					= Новый СтандартныйПериод;
	Период.ДатаНачала 		= НачалоМесяца( МесяцОценки );
	Период.ДатаОкончания 	= КонецМесяца( МесяцОценки );
	Результат = УП_КадрыСервер.СотрудникиПодразделенияЗаПериод(Подразделение, Период, ВключатьНепроизводственныеДолжности );
	Результат.Свернуть("ФизическоеЛицо", "");
	Результат.Колонки.Добавить("Флажок");
	Результат.ЗаполнитьЗначения( Истина, "Флажок");
	
	//
	Сотрудники.Загрузить(Результат);
	Сотрудники.Сортировать("ФизическоеЛицо");
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьСписокСотрудников(Команда)
	Если Подразделение.Пустая() Тогда
		ПоказатьПредупреждение(,"Укажите подразделение!",10);
		Возврат;
	КонецЕсли;
	
	ЗаполнитьСписокСотрудниковНаСервере();
КонецПроцедуры

&НаКлиенте
Процедура УстановитьЗначенияФлажков(ЗначФл)
	Для Каждого Сотр Из Сотрудники Цикл
		Сотр.флажок = ЗначФл;
	КонецЦикла;
КонецПроцедуры

&НаКлиенте
Процедура УстановитьФлажки(Команда)
	УстановитьЗначенияФлажков(Истина);
КонецПроцедуры

&НаКлиенте
Процедура СнятьФлажки(Команда)
	УстановитьЗначенияФлажков(Ложь);
КонецПроцедуры

&НаКлиенте
Процедура МесяцОценкиПриИзменении(Элемент)
	МесяцОценки = НачалоМесяца( МесяцОценки );
КонецПроцедуры
