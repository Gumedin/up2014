&НаСервере
Функция ПолучитьТаблицуАбонементов()
	//	1. составляем список продленных лицензий (на основании которых выписаны лицензии)
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ПродленныеЛицензии.Лицензия.Ссылка КАК Лицензия
		|ИЗ
		|	Документ.Лицензия КАК ПродленныеЛицензии
		|ГДЕ
		|	ПродленныеЛицензии.Лицензия.Ссылка <> &ПустаяЛицензия";

	Запрос.УстановитьПараметр("ПустаяЛицензия", Документы.Лицензия.ПустаяСсылка());
	
	Результат = Запрос.Выполнить().Выгрузить();
	Если Результат.Количество()> 0 Тогда
		мПродленныхЛицензий = Результат.ВыгрузитьКолонку("Лицензия");
		//Сообщить("количество продленных лицензий = " + мПродленныхЛицензий.Количество());
	Иначе
		мПродленныхЛицензий = Новый Массив;
	КонецЕсли;
	
	// 2.
    	//{{КОНСТРУКТОР_ЗАПРОСА_С_ОБРАБОТКОЙ_РЕЗУЛЬТАТА
	// Данный фрагмент построен конструктором.
	// При повторном использовании конструктора, внесенные вручную изменения будут утеряны!!!

	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	КонтрольныеЛицензии.Ссылка КАК Лицензия,
		|	КонтрольныеЛицензии.Организация,
		|	КонтрольныеЛицензии.Контрагент,
		|	КонтрольныеЛицензии.НеКонтролировать,
		|	КонтрольныеЛицензии.Дата КАК НачалоЛицензии,
		|	КонтрольныеЛицензии.Номер КАК НомерЛицензии,
		|	КонтрольныеЛицензии.ОкончаниеЛицензии КАК ОкончаниеЛицензии,
		|	КонтрольныеЛицензии.ВидНоменклатуры
		|ИЗ
		|	Документ.Лицензия КАК КонтрольныеЛицензии
		|ГДЕ
		|	(&Организация = ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка)
		|			ИЛИ КонтрольныеЛицензии.Организация = &Организация)
		|	И (&Контрагент = ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка)
		|			ИЛИ КонтрольныеЛицензии.Контрагент В ИЕРАРХИИ (&Контрагент))
		|	И (&ВидНоменклатуры = ЗНАЧЕНИЕ(Справочник.ВидыНоменклатуры.ПустаяСсылка)
		|			ИЛИ КонтрольныеЛицензии.ВидНоменклатуры = &ВидНоменклатуры)
		|	И КонтрольныеЛицензии.ОкончаниеЛицензии МЕЖДУ &НачПериода И &КонПериода
		|	И (НЕ КонтрольныеЛицензии.Ссылка В(&ПродленныеЛицензии))
		|
		|УПОРЯДОЧИТЬ ПО
		|	ОкончаниеЛицензии";

	Запрос.УстановитьПараметр("ВидНоменклатуры", 	ВидНоменклатуры);
	Запрос.УстановитьПараметр("Контрагент", 		Контрагент);
	Запрос.УстановитьПараметр("Организация", 		Организация);
	Запрос.УстановитьПараметр("НачПериода", 		ПериодКонтроля.ДатаНачала);
	Запрос.УстановитьПараметр("КонПериода", 		ПериодКонтроля.ДатаОкончания);
	Запрос.УстановитьПараметр("ПродленныеЛицензии", мПродленныхЛицензий);

	Результат = Запрос.Выполнить().Выгрузить();
	Возврат Результат;

КонецФункции

&НаСервере
Процедура КонтрольЛО_НаСервере( ТабДок )
	ПроцентЛО = 30;
	Макет = Обработки.КонтрольСроковЛО.ПолучитьМакет("КонтрольЛО");
	
	// абонементы
	тзАбонементов = ПолучитьТаблицуАбонементов();
	
	// начинаем выводить
	ОбластьШ	= Макет.ПолучитьОбласть("Шапка");
	ОбластьШ.Параметры.ПериодКонтроля 	= ПредставлениеПериода(ПериодКонтроля.ДатаНачала, 
											КонецДня(ПериодКонтроля.ДатаОкончания),"ФП=Истина");
	ОбластьШ.Параметры.ПечКонтрагенты 	= ?(Контрагент.Пустая(), 		"Все контрагенты", 	Контрагент.Наименование);
	ОбластьШ.Параметры.ПечОрганизации 	= ?(Организация.Пустая(), 		"Все организации", 	Организация.Наименование);
	ОбластьШ.Параметры.ПечТипЛицензии 	= ?(ВидНоменклатуры.Пустая(), 	"Все",				ВидНоменклатуры);
	ОбластьШ.Параметры.ПроцентЛО		= ПроцентЛО;
	
	ТабДок.Вывести(ОбластьШ);
	ИтСумма 	= 0;
	ИтСуммаЛО	= 0;
	
	Для Каждого Абонемент Из тзАбонементов Цикл
		СоставЛицензии 	= Абонемент.Лицензия.Состав;
		СуммаЛОАб		= 0; 
		
		Для Каждого СтрАб Из СоставЛицензии Цикл
			Если СоставЛицензии.Индекс( СтрАб ) = 0 Тогда
				
				ОбластьА							= Макет.ПолучитьОбласть("Абонемент");
				ОбластьА.Параметры.НомерАбонемента 	= тзАбонементов.Индекс( Абонемент ) + 1;
				ОбластьА.Параметры.Заполнить(Абонемент);
			Иначе
				ОбластьА	= Макет.ПолучитьОбласть("Спецификация");
			КонецЕсли;
			ОбластьА.Параметры.Заполнить(СтрАб);
			// ЛО
			СуммаЛО									= Окр( СтрАб.Сумма * ПроцентЛО/100, 2);
			СуммаЛОАб 								= СуммаЛОАб + СуммаЛО;
			ОбластьА.Параметры.СуммаЛО 				= СуммаЛО;
			//
			ОбластьА.Параметры.НаименованиеПолное 	= СтрАб.Номенклатура.НаименованиеПолное;
			ТабДок.Вывести(ОбластьА);
			
		КонецЦикла;
		ОбластьИтАб = Макет.ПолучитьОбласть("ИтогоПоАбонементу");
		ОбластьИтАб.Параметры.Количество 		= СоставЛицензии.Итог("Количество"); 
		ОбластьИтАб.Параметры.КоличествоПарус 	= СоставЛицензии.Итог("КоличествоПарус"); 
		ОбластьИтАб.Параметры.Сумма				= СоставЛицензии.Итог("Сумма"); 
		ОбластьИтАб.Параметры.СуммаЛО			= СуммаЛОАб; 
		// 
		ИтСумма 	= ИтСумма 	+ СоставЛицензии.Итог("Сумма");
		ИтСуммаЛО	= ИтСуммаЛО + СуммаЛОАб;
		ТабДок.Вывести(ОбластьИтАб);
		
	КонецЦикла;
	
	// всего	
	ОбластьП  = Макет.ПолучитьОбласть("Подвал");
	ОбластьП.Параметры.ИтСумма 		= ИтСумма;
	ОбластьП.Параметры.ИтСуммаЛО	= ИтСуммаЛО;
	ТабДок.Вывести(ОбластьП);
	
КонецПроцедуры


&НаКлиенте
Процедура КонтрольЛО(Команда)
	ТабДок = Новый ТабличныйДокумент;
	КонтрольЛО_НаСервере( ТабДок );
	
	ТабДок.ТолькоПросмотр 		= Истина;
	ТабДок.АвтоМасштаб 			= Истина;
	ТабДок.ОриентацияСтраницы 	= ОриентацияСтраницы.Ландшафт;
	
	ТабДок.Показать();
КонецПроцедуры
