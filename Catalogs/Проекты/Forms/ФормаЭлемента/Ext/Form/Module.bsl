&НаСервереБезКонтекста
Функция РасчитатьКодПроекта( Ссылка, Год  )
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ РАЗЛИЧНЫЕ ПЕРВЫЕ 1
		|	Проекты.Код КАК Код
		|ИЗ
		|	Справочник.Проекты КАК Проекты
		|ГДЕ
		|	Проекты.Код ПОДОБНО &ГодКода
		|
		|УПОРЯДОЧИТЬ ПО
		|	Код УБЫВ";

		
	МаскаКода 		= Формат( Год-2000, "ЧЦ=2; ЧДЦ=0; ЧВН=; ЧГ=") + "-";
	Запрос.УстановитьПараметр("ГодКода", МаскаКода + "%" );

	Результат = Запрос.Выполнить();

	ВыборкаДетальныеЗаписи = Результат.Выбрать();
	Если ВыборкаДетальныеЗаписи.Количество() = 0 Тогда
		Номер = 1;
	Иначе
		Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
			// вырезаем номер
			Номер = Число( Сред( ВыборкаДетальныеЗаписи.Код,4,3))+1;
		КонецЦикла;
	КонецЕсли;
	// новый код 
	Код  = МаскаКода + Формат( Номер, "ЧЦ=3; ЧДЦ=0; ЧВН=; ЧГ=");
	Возврат Код;
КонецФункции

&НаКлиенте
Процедура СформироватьКодПроекта(Команда)
	Объект.Код			= РасчитатьКодПроекта( Объект.Ссылка, Объект.ГодПроекта );
	Модифицированность = Истина;
КонецПроцедуры

&НаСервереБезКонтекста
Функция СписокНовыхСтатусов( ТекущийСтатус )
	ПраваФинансиста = Ложь;
	ПраваМенеджера 	= Ложь;
	
	
	СпСтатусов 		= Новый СписокЗначений;
	// доступна роль финанасиста
	Если 	РольДоступна(  Метаданные.Роли.ПолныеПрава ) или 
			РольДоступна(  Метаданные.Роли.ФинансовыйМенеджер ) Тогда
		ПраваФинансиста = Истина;
			
	КонецЕсли;
	// доступна роль клиент-менеджера
	Если 	РольДоступна(  Метаданные.Роли.ПолныеПрава ) или 
			РольДоступна(  Метаданные.Роли.КлиентМенеджер ) Тогда
		ПраваМенеджера 	= Истина;
	КонецЕсли;
	//Изменено по задаче #132349 15.08.2018 ГумединАГ	
	Если ТекущийСтатус = Перечисления.СтатусыПроектов.Прогноз и ПраваМенеджера Тогда
		СпСтатусов.Добавить( Перечисления.СтатусыПроектов.Черновик );
		
	ИначеЕсли ТекущийСтатус = Перечисления.СтатусыПроектов.Черновик и ПраваМенеджера Тогда
		СпСтатусов.Добавить( Перечисления.СтатусыПроектов.Согласование );
		
	ИначеЕсли ТекущийСтатус = Перечисления.СтатусыПроектов.Согласование и ПраваФинансиста Тогда
		СпСтатусов.Добавить( Перечисления.СтатусыПроектов.ВРаботе );
		СпСтатусов.Добавить( Перечисления.СтатусыПроектов.Корректировка );
	
	ИначеЕсли ТекущийСтатус = Перечисления.СтатусыПроектов.ВРаботе и ПраваФинансиста Тогда
		СпСтатусов.Добавить( Перечисления.СтатусыПроектов.Заблокирован );
		
	ИначеЕсли ТекущийСтатус = Перечисления.СтатусыПроектов.ВРаботе и ПраваМенеджера Тогда
		СпСтатусов.Добавить( Перечисления.СтатусыПроектов.Корректировка );	
	
	ИначеЕсли ТекущийСтатус = Перечисления.СтатусыПроектов.Заблокирован и ПраваФинансиста Тогда
		СпСтатусов.Добавить( Перечисления.СтатусыПроектов.Согласование );
		
	ИначеЕсли ТекущийСтатус = Перечисления.СтатусыПроектов.Корректировка и ПраваМенеджера Тогда
		СпСтатусов.Добавить( Перечисления.СтатусыПроектов.Согласование );
			
	КонецЕсли;
	Возврат СпСтатусов;
	
КонецФункции

&НаКлиенте
Процедура ИзменитьСтатусПроекта( Результат, ДопПараметры )  Экспорт
	Если Результат <> КодВозвратаДиалога.Да Тогда 
		Возврат;
	КонецЕсли;
	
	сСтатусов = СписокНовыхСтатусов( Объект.Статус );
	Если 		сСтатусов.Количество() = 0 Тогда 
		ПоказатьПредупреждение(,"Не разрешено изменить статус проекта!", 10);
		
	ИначеЕсли	сСтатусов.Количество() = 1 Тогда 
		Объект.Статус = сСтатусов.Получить(0).Значение;
		
	Иначе
		Эл = сСтатусов.ВыбратьЭлемент( "Выберите новый статус проекта");
		Если Эл <> Неопределено Тогда
			Объект.Статус = Эл.Значение;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ИзменитьСтатус(Команда)
	
	ДопПараметры = Новый Структура;
	Опов = Новый ОписаниеОповещения("ИзменитьСтатусПроекта", ЭтаФорма, ДопПараметры);
	ПоказатьВопрос(Опов, "Изменить статус проекта?", РежимДиалогаВопрос.ДаНет);
	
		
	
КонецПроцедуры

&НаКлиенте
Процедура СформироватьНаименованиеПроекта(Команда)
	Объект.Наименование = Формат( Объект.Код, "ЧГ=") + ", " + СокрЛП( Объект.Контрагент);
	Модифицированность = Истина;
КонецПроцедуры



&НаСервереБезКонтекста
Функция ДоступностьЗадачиПроекта( РольУчастника )
	Возврат РольУчастника.ПоЗадачеПроекта;
КонецФункции

&НаКлиенте
Процедура УчастникиПроектаПередНачаломИзменения(Элемент, Отказ)
	Стр = Элемент.ТекущиеДанные;
	Если Элемент.ТекущийЭлемент.Имя = "УчастникиПроектаЗадачаПроекта" И 
	НЕ ДоступностьЗадачиПроекта( Стр.Роль ) Тогда
		Отказ = Истина;
		Возврат;
	КонецЕсли;
КонецПроцедуры
