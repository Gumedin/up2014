Процедура Печать(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода) Экспорт
	
	// печать счёта на оплату
	ПодготовитьПечатнуюФорму(
		"РасходПоЗадаче",
		НСтр("ru = 'Расход по задаче'"),
		"ОбщиеМакеты.ПФ_MXL_РасходПоЗадаче",
		МассивОбъектов,
		КоллекцияПечатныхФорм,
		ОбъектыПечати,
		ПараметрыВывода);
	
		
	ПараметрыВывода.ДоступнаПечатьПоКомплектно = Ложь;
	
КонецПроцедуры

Процедура ПодготовитьПечатнуюФорму(Знач ИмяМакета, ПредставлениеМакета, ПолныйПутьКМакету = "", МассивОбъектов, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода)
	
	НужноПечататьМакет = УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, ИмяМакета);
	Если НужноПечататьМакет Тогда
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(
		КоллекцияПечатныхФорм,
		ИмяМакета,
		ПредставлениеМакета,
		ПечатьРасходаПоЗадаче(МассивОбъектов, ОбъектыПечати, ИмяМакета),
		,
		ПолныйПутьКМакету);
	КонецЕсли;
	
КонецПроцедуры


Функция ПечатьРасходаПоЗадаче(МассивОбъектов, ОбъектыПечати, ИмяМакета)
	Запрос = Новый Запрос;

	Запрос.Текст = 
		"ВЫБРАТЬ
		|	РасходПоЗадачеПроекта.Ссылка
		|ИЗ
		|	Документ.РасходПоЗадачеПроекта КАК РасходПоЗадачеПроекта
		|ГДЕ
		|	РасходПоЗадачеПроекта.Ссылка В(&МассивОбъектов)
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	РасходПоЗадачеПроизводство.Ссылка
		|ИЗ
		|	Документ.РасходПоЗадачеПроизводство КАК РасходПоЗадачеПроизводство
		|ГДЕ
		|	РасходПоЗадачеПроизводство.Ссылка В(&МассивОбъектов)
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	КалькуляторРасходаПоЗадаче.Ссылка
		|ИЗ
		|	Справочник.КалькуляторРасходаПоЗадаче КАК КалькуляторРасходаПоЗадаче
		|ГДЕ
		|	КалькуляторРасходаПоЗадаче.Ссылка В(&МассивОбъектов)";
	
	
	Запрос.УстановитьПараметр("МассивОбъектов", МассивОбъектов);
	Шапка = Запрос.Выполнить().Выбрать();
	
	ТабличныйДокумент = Новый ТабличныйДокумент;
	ТабличныйДокумент.КлючПараметровПечати = "РасходПоЗадачеПроекта";
	
	Макет = УправлениеПечатью.ПолучитьМакет("ОбщиеМакеты.ПФ_MXL_РасходПоЗадаче");
	
	Пока Шапка.Следующий() Цикл
		Если ТабличныйДокумент.ВысотаТаблицы > 0 Тогда
			ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
		КонецЕсли;
		
		НомерСтрокиНачало = ТабличныйДокумент.ВысотаТаблицы + 1;
		
		// выводим документ
		СформироватьРасходПоИсточнику(ТабличныйДокумент, Шапка.Ссылка, Макет );
		
		// конец вывода документа

		УправлениеПечатью.ЗадатьОбластьПечатиДокумента(ТабличныйДокумент, НомерСтрокиНачало, ОбъектыПечати, Шапка.Ссылка);
	
	КонецЦикла;
	
	Возврат ТабличныйДокумент;
	
КонецФункции

Процедура СформироватьРасходПоИсточнику(ТабДок, Ссылка, Макет )
	
	
	// начальный фронт работ
	Если 		ТипЗнч( Ссылка )= Тип("ДокументСсылка.РасходПоЗадачеПроекта" ) Тогда
		Источник 		= "Документ.РасходПоЗадачеПроекта.Описание";
		Подразделение	= Ссылка.ЗадачаПроекта.Подразделение;
		Проект	 		= Ссылка.ЗадачаПроекта.Владелец;
		ЗадачаПроекта 	= Ссылка.ЗадачаПроекта;
		Заголовок		= "Расход по задаче проекта № " + Ссылка.Номер + " от " + Формат(Ссылка.Дата,"ДЛФ=D"); 
		
	ИначеЕсли 	ТипЗнч( Ссылка )= Тип("ДокументСсылка.РасходПоЗадачеПроизводство" ) Тогда
		Источник 		= "Документ.РасходПоЗадачеПроизводство.Описание";
		Подразделение	= Ссылка.ЗадачаПроекта.Подразделение;
		Проект	 		= Ссылка.ЗадачаПроекта.Владелец;
		ЗадачаПроекта 	= Ссылка.ЗадачаПроекта;
		Заголовок		= "Расход по задаче проекта (производство) № " + Ссылка.Номер + " от " + Формат(Ссылка.Дата,"ДЛФ=D"); 
		
	ИначеЕсли 	ТипЗнч( Ссылка )= Тип("СправочникСсылка.КалькуляторРасходаПоЗадаче" ) Тогда
		Источник 		= "Справочник.КалькуляторРасходаПоЗадаче.Описание";
		Подразделение	= Ссылка.Подразделение;
		Проект	 		= Ссылка.Родитель.Наименование; 
		ЗадачаПроекта 	= Ссылка.Наименование;
		Заголовок		= "Калькулятор расхода по задаче проекта";
		
	КонецЕсли;
	
	Шапка = Макет.ПолучитьОбласть("Шапка");
	Шапка.Параметры.Заголовок 		= Заголовок;
	Шапка.Параметры.ЗадачаПроекта 	= ЗадачаПроекта;
	Шапка.Параметры.Проект 			= Проект;
	Шапка.Параметры.Подразделение 	= Подразделение;
	Шапка.Параметры.СтатьяСметы 	= Ссылка.СтатьяСметы;
	
	ТабДок.Вывести(Шапка );
	
	Для Каждого СтрРасход ИЗ Ссылка.Описание Цикл
		ОблРасход	= Макет.ПолучитьОбласть("Расход");
		ЗаполнитьЗначенияСвойств( ОблРасход.Параметры, СтрРасход);
		ТабДок.Вывести(ОблРасход);
	КонецЦикла;
	
	Подвал = Макет.ПолучитьОбласть("Подвал");
	тз = Ссылка.Описание.Выгрузить();
	ЗаполнитьЗначенияСвойств( Подвал.Параметры, Ссылка );
	//
	СуммаИтого = тз.Итог("Сумма");
	Подвал.Параметры.Сумма 				= СуммаИтого;
	СуммаКонвертацииИтого				= тз.Итог("СуммаКонвертации");
	Подвал.Параметры.СуммаКонвертации 	= СуммаКонвертацииИтого;
	//
	Рубль 								= Справочники.Валюты.НайтиПоКоду("643");
	Подвал.Параметры.ВсегоПодокументу 	= РаботаСКурсамиВалют.СформироватьСуммуПрописью(СуммаИтого + СуммаКонвертацииИтого, Рубль);

	ТабДок.Вывести(Подвал);
	
	
КонецПроцедуры


