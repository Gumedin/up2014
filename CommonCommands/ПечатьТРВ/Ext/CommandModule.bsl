&НаСервере
Функция СтруктураПараметровПоТабелю( ПараметрКоманды )
	СтрП = Новый Структура;
	СтрП.Вставить("ПериодРегистрации", 	ПараметрКоманды.ПериодРегистрации);
	СтрП.Вставить("ФизическоеЛицо", 	ПараметрКоманды.ФизическоеЛицо);
	
	Возврат СтрП;
	
КонецФункции

&НаКлиенте
Процедура ВывестиОтчет( Период, ПараметрыОповещения )
	Если Период = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("Период", 			Период );
	ДополнительныеПараметры.Вставить("ТипПараметра", 	ПараметрыОповещения.ТипПараметра );
	ДополнительныеПараметры.Вставить("ФизическоеЛицо", 	ПараметрыОповещения.ФизическоеЛицо );
	
	ПараметрыПечати = Новый Структура;
	ПараметрыПечати.Вставить("ДополнительныеПараметры", ДополнительныеПараметры );
	ПараметрыПечати.Вставить("ЗаголовокФормы", 			ПараметрыОповещения.ЗаголовокФормы );
	
	УправлениеПечатьюКлиент.ВыполнитьКомандуПечати("Отчет.ПечатьТабеляРабочегоВремени", "ТабельРВ", 
			ПараметрыОповещения.МассивОбъектов, ПараметрыОповещения.Источник, ПараметрыПечати);	
КонецПроцедуры

&НаКлиенте
Процедура ВыполнитьКомандуПечати(ПараметрКоманды, Форма) Экспорт
	ПараметрыОповещения = Новый  Структура;
	
	ПараметрыОповещения.Вставить("МассивОбъектов", ПараметрКоманды);
	ПараметрыОповещения.Вставить("Источник",  Форма);
	
	Если ТипЗнч(ПараметрКоманды[0]) = Тип("ДокументСсылка.ТабельУчетаРабочегоВремени") Тогда
		СтрПар = СтруктураПараметровПоТабелю( ПараметрКоманды[0] );
		Период = Новый СтандартныйПериод;
		Период.ДатаНачала 		= НачалоМесяца( СтрПар.ПериодРегистрации );
		Период.ДатаОкончания 	= КонецМесяца( СтрПар.ПериодРегистрации );
		
		ПараметрыОповещения.Вставить("ФизическоеЛицо",  СтрПар.ФизическоеЛицо);
		ПараметрыОповещения.Вставить("ТипПараметра", 1 );
		ПараметрыОповещения.Вставить("ЗаголовокФормы", "Табель РВ за " + ПредставлениеПериода(Период.ДатаНачала, Период.ДатаОкончания ) + 
									  ". "+ СтрПар.ФизическоеЛицо ); 
		
		ВывестиОтчет( Период, ПараметрыОповещения );
		
	Иначе
		// для физического лица
		Если ТипЗнч(ПараметрКоманды[0]) = Тип("СправочникСсылка.ФизическиеЛица") Тогда
			ПараметрыОповещения.Вставить("ТипПараметра", 1 );
			ПараметрыОповещения.Вставить("ФизическоеЛицо",  ПараметрКоманды );
		Иначе
			ПараметрыОповещения.Вставить("ТипПараметра", 2 );
			ПараметрыОповещения.Вставить("ФизическоеЛицо",  "" );
		КонецЕсли;
		ПараметрыОповещения.Вставить("Основание",  ПараметрКоманды );
		ПараметрыОповещения.Вставить("ЗаголовокФормы", "Табель работ по " + ПараметрКоманды ); 
		
		// 		
		Диалог = Новый ДиалогРедактированияСтандартногоПериода;
		Диалог.Период.Вариант = ВариантСтандартногоПериода.ПрошлыйМесяц;
		
		ОписаниеОповещения = Новый ОписаниеОповещения("ВывестиОтчет", ЭтотОбъект, ПараметрыОповещения );
		Диалог.Показать(ОписаниеОповещения);
	КонецЕсли;
КонецПроцедуры	

&НаКлиенте
Процедура ОбработкаКоманды(ПараметрКоманды, ПараметрыВыполненияКоманды)
	УправлениеПечатьюКлиент.ПроверитьПроведенностьДокументов( 
		Новый ОписаниеОповещения("ВыполнитьКомандуПечати", ЭтотОбъект, ПараметрыВыполненияКоманды.Источник), 
		ПараметрКоманды);	
КонецПроцедуры
