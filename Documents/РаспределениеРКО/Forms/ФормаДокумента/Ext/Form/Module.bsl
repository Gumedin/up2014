&НаСервереБезКонтекста
Функция ПолучитьСтруктуруСметы( Дата )
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	СтруктураСметыПоГодамСрезПоследних.СтруктураСметы
		|ИЗ
		|	РегистрСведений.СтруктураСметыПоГодам.СрезПоследних(&МесяцНачисления, ) КАК СтруктураСметыПоГодамСрезПоследних";
	
	Запрос.УстановитьПараметр("МесяцНачисления", Дата);
	
	РезультатЗапроса = Запрос.Выполнить();
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		Возврат ВыборкаДетальныеЗаписи.СтруктураСметы;
	КонецЦикла;
	Возврат Неопределено;
КонецФункции

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	Если НЕ ЗначениеЗаполнено( Объект.МесяцНачисления ) Тогда
		Объект.МесяцНачисления = НачалоМесяца( ТекущаяДата());
	КонецЕсли;
КонецПроцедуры

&НаСервере
Функция БазаЗаМесяцПоСтатье( СтатьиСметы, Месяц, ПоОплате = Ложь )

	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	СУММА(БюджетПоМесяцам.Сумма) КАК БазаРаспределения,
		|	БюджетПоМесяцам.ЗадачаПроекта,
		|	СУММА(0) КАК СуммаРКО
		|ИЗ
		|	РегистрНакопления.БюджетПоМесяцам КАК БюджетПоМесяцам
		|ГДЕ
		|	БюджетПоМесяцам.РКО
		|	И БюджетПоМесяцам.СтатьяСметы В(&СтатьиСметы)
		|	И ВЫБОР
		|			КОГДА &ПоОплате
		|				ТОГДА НАЧАЛОПЕРИОДА(БюджетПоМесяцам.Период, МЕСЯЦ) = &Месяц
		|			ИНАЧЕ БюджетПоМесяцам.Месяц = &Месяц
		|		КОНЕЦ
		|	И БюджетПоМесяцам.ТипСуммы = &ТипСуммы
		|	И ТИПЗНАЧЕНИЯ(БюджетПоМесяцам.Регистратор) <> ТИП(Документ.РаспределениеРКО)
		|
		|СГРУППИРОВАТЬ ПО
		|	БюджетПоМесяцам.ЗадачаПроекта
		|
		|ИМЕЮЩИЕ
		|	СУММА(БюджетПоМесяцам.Сумма) <> 0";

	Запрос.УстановитьПараметр("Месяц", 			Месяц);
	Запрос.УстановитьПараметр("ТипСуммы", 		Перечисления.ТипСуммыБюджета.Факт );
	Запрос.УстановитьПараметр("СтатьиСметы", 	СтатьиСметы);
	Запрос.УстановитьПараметр("ПоОплате", 		ПоОплате);

	Результат = Запрос.Выполнить().Выгрузить();
	Возврат Результат;

КонецФункции

&НаКлиенте
Процедура РаспределениеПослеУдаления(Элемент)
	РаспределитьПоСтатьеНаСервере( Ложь );
КонецПроцедуры

&НаКлиенте
Процедура СдвигПриИзменении(Элемент)
	УстановитьМесяцБазы();
КонецПроцедуры

&НаКлиенте
Процедура УстановитьМесяцБазы()
	МесяцБазы = ДобавитьМесяц( НачалоМесяца( Объект.МесяцНачисления), Объект.Сдвиг );
КонецПроцедуры

&НаКлиенте
Процедура МесяцНачисленияПриИзменении(Элемент)
	Объект.МесяцНачисления = НачалоМесяца( Объект.МесяцНачисления);
	
	УстановитьМесяцБазы();
КонецПроцедуры

&НаСервереБезКонтекста
Функция  ПолучитьСтатьиРКО( СтруктураСметы )
	мСтатей = СтруктураСметы.РаспределениеРКО.Выгрузить().ВыгрузитьКолонку("СтатьяРКО");
	Возврат мСтатей;
	
КонецФункции

&НаКлиенте
Процедура УстановитьСписокВыбораСтатейРКО()
	Элементы.Статья.РежимВыбораИзСписка=Истина;
	СтруктураСметы 	= ПолучитьСтруктуруСметы( Объект.МесяцНачисления );
	МассивСтатей 	= ПолучитьСтатьиРКО( СтруктураСметы );
	
	Элементы.Статья.СписокВыбора.ЗагрузитьЗначения( МассивСтатей );
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	УстановитьМесяцБазы();
	УстановитьСписокВыбораСтатейРКО();
КонецПроцедуры



#Область НовоеРаспределение

&НаКлиенте
Процедура РаспределитьПоСтатье(Команда)
	Если НЕ ЗначениеЗаполнено( Объект.МесяцНачисления ) Тогда
		ПоказатьПредупреждение(,"Укажите месяц начисления!", 10);
		Возврат;
	КонецЕсли;
	Если НЕ ЗначениеЗаполнено( Объект.Статья ) Тогда
		ПоказатьПредупреждение(,"Укажите статью начисления РКО!", 10);
		Возврат;
	КонецЕсли;
	Если НЕ ЗначениеЗаполнено( Объект.СтатьяБазы ) Тогда
		ПоказатьПредупреждение(,"Укажите статью для расчета базы начисления РКО!", 10);
		Возврат;
	КонецЕсли;
	Если НЕ ЗначениеЗаполнено( Объект.СуммаРКО ) Тогда
		ПоказатьПредупреждение(,"Укажите сумму для распределения!", 10);
		Возврат;
	КонецЕсли;
	// 
	РаспределитьПоСтатьеНаСервере( Истина );
КонецПроцедуры

&НаСервере
Процедура РаспределитьПоСтатьеНаСервере( ЗаполнитьБазуРаспределения = Ложь )
	
	ДокОб = РеквизитФормыВЗначение("Объект");
	Если ЗаполнитьБазуРаспределения Тогда
		ДокОб.Распределение.Очистить();
	КонецЕсли;
	
	// 
	МесяцБазы	= ДобавитьМесяц( Объект.МесяцНачисления, Объект.Сдвиг );
		
	Если ЗаполнитьБазуРаспределения Тогда
		// база изначально
		тзБазаРаспр = БазаЗаМесяцПоСтатье( Объект.СтатьяБазы, МесяцБазы, Объект.ПоОплате );	
	Иначе
		// список задача проектов не перезаполняется
		тзБазаРаспр = ДокОб.Распределение.Выгрузить(); 
			
	КонецЕсли;
		
	// коэффициенты 
	мКоэффСтатьи	= Новый Массив;
	Для Каждого Стр ИЗ тзБазаРаспр Цикл
		мКоэффСтатьи.Добавить( Стр.БазаРаспределения );
	КонецЦикла;
	
	// распределяем по задачам проекта		
	мРКО	= РаспределитьПропорционально( Объект.СуммаРКО, мКоэффСтатьи );
	//
	Для Каждого Стр из тзБазаРаспр Цикл
		ИндексСтроки = тзБазаРаспр.Индекс( Стр );
		
		Если ЗаполнитьБазуРаспределения Тогда
			СтрДок 	= ДокОб.Распределение.Добавить();
			СтрДок.ЗадачаПроекта		= Стр.ЗадачаПроекта;
			//СтрДок.Статья				= Объект.Статья;
			СтрДок.БазаРаспределения	= Стр.БазаРаспределения;
		Иначе
			СтрДок  = ДокОб.Распределение.Получить(ИндексСтроки);
		КонецЕсли;
		
		СтрДок.СуммаРКО 			= мРКО[ИндексСтроки];
		
	КонецЦикла;
	
	// Записываем в таблицу документа 
	ЗначениеВРеквизитФормы(ДокОб, "Объект");
	
КонецПроцедуры

&НаКлиенте
Процедура СтатьяПриИзменении(Элемент)
	СтруктураСметы 		= ПолучитьСтруктуруСметы( Объект.МесяцНачисления );
	сПР = НайтиПараметрыРКО( СтруктураСметы, Объект.Статья  );
	ЗаполнитьЗначенияСвойств( Объект, сПР );
КонецПроцедуры

&НаСервереБезКонтекста
Функция НайтиПараметрыРКО( СтруктураСметы, Статья )
	Стр = СтруктураСметы.РаспределениеРКО.Найти( Статья, "СтатьяРКО");
	сПР = Новый Структура;
	Если Стр = Неопределено Тогда
		сПР.Вставить("Сдвиг", 		0 );
		сПР.Вставить("СтатьяБазы", 	Справочники.СтатьиСметы.ПустаяСсылка());
		сПР.Вставить("ПоОплате", 	Ложь);
	Иначе
		сПР.Вставить("Сдвиг", 		Стр.Сдвиг );
		сПР.Вставить("СтатьяБазы", 	Стр.СтатьяБазыРаспределения );
		сПР.Вставить("ПоОплате", 	Стр.ПоОплате );
	КонецЕсли;
	Возврат сПР;
КонецФункции


#КонецОбласти
