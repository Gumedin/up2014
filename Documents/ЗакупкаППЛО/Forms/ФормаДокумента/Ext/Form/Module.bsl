&НаКлиенте
Процедура ПодсчетИтого()
	ИтогоВендору 	= Объект.Товар.Итог("СуммаПравоОбл") - Объект.СуммаДопОтВендора;
	ИтогоОтКлиента 	= Объект.СуммаДопОтКлиента + Объект.Товар.Итог("СуммаПП");
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция  ПолучитьОтпускнуюЦену( Номенклатура, Дата)
	// 
	Возврат ЦенаНоменклатуры( Номенклатура, Справочники.ВидыЦен.ЦенаОтпускная, Дата );
КонецФункции

&НаСервереБезКонтекста
Функция  ПолучитьСкидку( Номенклатура )
	Возврат Номенклатура.СкидкаНаППЛО.Скидка;
КонецФункции

&НаКлиенте
Процедура ПересчетСтроки( Стр )
	Стр.СуммаПП 		= Стр.Цена 		* Стр.Количество;
	Стр.СуммаПравоОбл 	= Стр.СуммаПП	* (1 - Стр.Скидка / 100);
	Стр.СуммаДоход 		= Стр.СуммаПП 	- Стр.СуммаПравоОбл;
КонецПроцедуры

&НаКлиенте
Процедура ТоварЦенаПриИзменении(Элемент)
	Стр = Элементы.Товар.ТекущиеДанные;
	ПересчетСтроки( Стр );
КонецПроцедуры

&НаКлиенте
Процедура ТоварКоличествоПриИзменении(Элемент)
	Стр = Элементы.Товар.ТекущиеДанные;
	ПересчетСтроки( Стр );
КонецПроцедуры

&НаКлиенте
Процедура ТоварНоменклатураПриИзменении(Элемент)
	Стр 		= Элементы.Товар.ТекущиеДанные;
	Стр.Цена 	= ПолучитьОтпускнуюЦену( Стр.Номенклатура,	Объект.ДатаОплатыПравообладателю );
	Стр.Скидка  = ПолучитьСкидку( Стр.Номенклатура );
	ПересчетСтроки( Стр );

КонецПроцедуры

&НаКлиенте
Процедура ПроцентВендораПриИзменении(Элемент)
	//Для Каждого Стр ИЗ Объект.Товар Цикл
	//	ПересчетСтроки( Стр );
	//КонецЦикла;
КонецПроцедуры

&НаКлиенте
Процедура ТоварПриИзменении(Элемент)
	ПодсчетИтого();
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	ПодсчетИтого();
КонецПроцедуры

&НаКлиенте
Процедура СуммаДопОтКлиентаПриИзменении(Элемент)
	ПодсчетИтого();
КонецПроцедуры

&НаКлиенте
Процедура СуммаДопОтВендораПриИзменении(Элемент)
	ПодсчетИтого();
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	Если НЕ РазрешеноСоздаватьДокумент( Объект.Ссылка, Объект.ЗадачаПроекта.Владелец ) Тогда
		Отказ = Истина;
		Возврат;
	КонецЕсли;		
КонецПроцедуры


//*******************************************************************************************
//	Заполнение из калькулятора
 
&НаСервере
Процедура ЗаполнитьИзКалькулятораНаСервере( Калькулятор )
	// ничего не проверяем проверяем 
	Док = РеквизитФормыВЗначение("Объект");
	ЗаполнитьЗначенияСвойств( Док, Калькулятор );
	Товар = Калькулятор.Товар.Выгрузить();
	Док.Товар.Загрузить( Товар );
	
	ЗначениеВРеквизитФормы( Док, "Объект");
	ЭтаФорма.Модифицированность = Истина;
КонецПроцедуры


&НаКлиенте
Процедура ПослеЗакрытияВопроса(Результат, Калькулятор ) Экспорт
	Если Результат = КодВозвратаДиалога.Да Тогда
		ЗаполнитьИзКалькулятораНаСервере( Калькулятор );
	КонецЕсли; 
КонецПроцедуры

&НаКлиенте
Процедура ПодтвердитьЗаполнение(Калькулятор, ДопПараметры ) Экспорт
	Если Калькулятор = Неопределено Тогда Возврат; КонецЕсли;
	
	ОпОповещения = Новый ОписаниеОповещения("ПослеЗакрытияВопроса", ЭтаФорма, Калькулятор  );
	ПоказатьВопрос(ОпОповещения, "Заполнить текущий документ из выбранного калькулятора?",
						РежимДиалогаВопрос.ДаНет);
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьИзКалькулятора(Команда)
	Перем Калькулятор;
	Если Объект.Проведен Тогда
		ПоказатьПредупреждение(,"Заполнить из калькулятора можно только непроведенный документ!");
		Возврат;
	КонецЕсли;
	
	ОпОповещения = Новый ОписаниеОповещения("ПодтвердитьЗаполнение", ЭтаФорма );
	ПоказатьВводЗначения( ОпОповещения, Калькулятор,, Тип("СправочникСсылка.КалькуляторСтоимостиППЛО"));
КонецПроцедуры

