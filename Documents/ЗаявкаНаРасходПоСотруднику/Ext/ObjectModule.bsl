
Процедура ОбработкаЗаполнения(ДанныеЗаполнения, СтандартнаяОбработка)
	ИсполнительДокумента 	= ПараметрыСеанса.ТекущийПользователь;
	ПериодРегистрации		= НачалоМесяца( ТекущаяДата());
	СтатьяСметы				= Справочники.СтатьиСметы.РасходыППпоПроекту;
	ФизическоеЛицо			= ИсполнительДокумента.ФизическоеЛицо;
	мПодразделения 			= УП_КадрыСервер.ГдеРаботает( ФизическоеЛицо );
	Если мПодразделения.Количество() <> 0 Тогда
		Подразделение = мПодразделения[0];
	КонецЕсли;
	
	
КонецПроцедуры

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	Если УП_ПереносДанных.ПроведениеПриПереносе() Тогда
		Возврат;
	КонецЕсли;
	
	Если РежимЗаписи = РежимЗаписиДокумента.Проведение 
	И	НЕ ВсеЗадачиВУказанномСтатусе( Перечисления.СтатусыПроектов.ВРаботе, 
										Ссылка.Расходы.Выгрузить(, "ЗадачаПроекта") ) Тогда
		Сообщить("Не все проекты в статусе " + Перечисления.СтатусыПроектов.ВРаботе );
		Отказ = Истина;
		Возврат;
	КонецЕсли;

КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, Режим)
	// регистр БюджетПоМесяцам 
	Движения.БюджетПоМесяцам.Записывать = Истина;
	Для Каждого ТекСтрокаРаспределение Из Расходы Цикл
		Если ТекСтрокаРаспределение.Сумма = 0 Тогда Продолжить; КонецЕсли;
		
		//Структура сметы
		СметаЗадачиПроекта = УП_СметаПроекта.ПодборСметыЗадачи(ТекСтрокаРаспределение.ЗадачаПроекта);
		Если СметаЗадачиПроекта = Неопределено Тогда 
			Сообщить("Не найдена проведенная смета для задачи " + ТекСтрокаРаспределение.ЗадачаПроекта);
			Отказ = Истина;
			Возврат;
		КонецЕсли;
		сСметы 	= УП_СметаПроекта.ПолучитьСтруктуруСметыЗадачи( ТекСтрокаРаспределение.ЗадачаПроекта.ГодЗадачи );

		сСтатей = Новый Соответствие;
		пСтатей = Новый Соответствие;
		oСтатей = Новый Соответствие;
		Для Каждого статья Из СметаЗадачиПроекта.Расчет Цикл
			сСтатей[статья.ИмяСтатьи] = 0;
			пСтатей[статья.ИмяСтатьи] = статья.ЗначениеПараметра / 100;
			oСтатей[статья.ИмяСтатьи] = статья.Статья;
		КонецЦикла;
		
		статья = СтатьяСметы.ИмяПредопределенныхДанных;
		сСтатей[статья] = ТекСтрокаРаспределение.Сумма; 
		
		//Пересчет
		УП_СметаПроекта.ПересчетСметыЗадачи( сСметы, сСтатей );
		
		//Бюджет факт
		Для Каждого статья Из СметаЗадачиПроекта.Расчет Цикл
			Если сСтатей[статья.ИмяСтатьи] <> 0 Тогда 
				Движение = Движения.БюджетПоМесяцам.Добавить();
				Движение.Период 		= Дата;
				Движение.ЗадачаПроекта 	= ТекСтрокаРаспределение.ЗадачаПроекта;
				Движение.СтатьяСметы 	= oСтатей[статья.ИмяСтатьи];
				Движение.ТипСуммы 		= Перечисления.ТипСуммыБюджета.Факт;  // факт
				Движение.Месяц 			= НачалоМесяца(ПериодРегистрации);
				Движение.Сумма          = сСтатей[статья.ИмяСтатьи];	
				Движение.РКО			= Истина;
			КонецЕсли;
		КонецЦикла;
	КонецЦикла;
КонецПроцедуры
