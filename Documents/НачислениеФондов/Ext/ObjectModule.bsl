
Процедура ОбработкаЗаполнения(ДанныеЗаполнения, СтандартнаяОбработка)
	ИсполнительДокумента 	= ПараметрыСеанса.ТекущийПользователь;
	СтатьяБазыРаспределения = Справочники.СтатьиСметы.ДоходОтРеализации;
	ГодПроектов				= Год( ТекущаяДата());
	ПериодРегистрацииНачало 	= НачалоМесяца( ТекущаяДата());
	ПериодРегистрацииОкончание 	= ПериодРегистрацииНачало;

КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, Режим)
	// регистр БюджетПоМесяцам 
	Движения.БюджетПоМесяцам.Записывать = Истина;
	Для Каждого ТекСтрокаРаспределение Из Начисления Цикл
		Если ТекСтрокаРаспределение.Сумма = 0 Тогда Продолжить; КонецЕсли;
		
		//Структура сметы
		СметаЗадачиПроекта = УП_СметаПроекта.ПодборСметыЗадачи(ТекСтрокаРаспределение.ЗадачаПроекта);
		Если СметаЗадачиПроекта = Неопределено Тогда 
			Сообщить("Не найдена проведенная смета для задачи " + ТекСтрокаРаспределение.ЗадачаПроекта);
			Отказ = Истина;
			Возврат;
		КонецЕсли;
		сСметы 	= УП_СметаПроекта.ПолучитьСтруктуруСметыЗадачи( ТекСтрокаРаспределение.ЗадачаПроекта.ГодЗадачи );

		сСтатей = Новый Соответствие;
		пСтатей = Новый Соответствие;
		oСтатей = Новый Соответствие;
		Для Каждого статья Из СметаЗадачиПроекта.Расчет Цикл
			сСтатей[статья.ИмяСтатьи] = 0;
			пСтатей[статья.ИмяСтатьи] = статья.ЗначениеПараметра / 100;
			oСтатей[статья.ИмяСтатьи] = статья.Статья;
		КонецЦикла;
		
		статья = СтатьяФонда.ИмяПредопределенныхДанных;
		сСтатей[статья] = ТекСтрокаРаспределение.Сумма; 
		
		//Пересчет
		УП_СметаПроекта.ПересчетСметыЗадачи( сСметы, сСтатей );
		
		//Бюджет факт
		Для Каждого статья Из СметаЗадачиПроекта.Расчет Цикл
			Если сСтатей[статья.ИмяСтатьи] <> 0 Тогда 
				Движение = Движения.БюджетПоМесяцам.Добавить();
				Движение.Период 		= Дата;
				Движение.ЗадачаПроекта 	= ТекСтрокаРаспределение.ЗадачаПроекта;
				Движение.СтатьяСметы 	= oСтатей[статья.ИмяСтатьи];
				Движение.ТипСуммы 		= Перечисления.ТипСуммыБюджета.Факт;  // факт
				Движение.Месяц 			= НачалоМесяца(ТекСтрокаРаспределение.ПериодРегистрации);
				Движение.Сумма          = сСтатей[статья.ИмяСтатьи];	
				Движение.РКО			= Истина;
			КонецЕсли;
		КонецЦикла;
	КонецЦикла;
КонецПроцедуры
