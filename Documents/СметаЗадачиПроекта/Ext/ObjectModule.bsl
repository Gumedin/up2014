
Процедура ОбработкаЗаполнения(ДанныеЗаполнения, СтандартнаяОбработка)
	Если ТипЗнч(ДанныеЗаполнения) = Тип("СправочникСсылка.ЗадачиПроектов") Тогда
		// Заполнение шапки
		ЗадачаПроекта = ДанныеЗаполнения.Ссылка;
	КонецЕсли;
	ИсполнительДокумента = ПараметрыСеанса.ТекущийПользователь;
КонецПроцедуры

// 
Функция РаспределитьРавномерноПоПериодуЗадачи( Статья, Сумма )
	мМесяцыЗадачи = УП_ПланыРаботПоПроектам.МесяцыЗадачиПроекта( ЗадачаПроекта );
	тзПоМесяцам = Новый ТаблицаЗначений;
	тзПоМесяцам.Колонки.Добавить("Месяц");
	тзПоМесяцам.Колонки.Добавить("Сумма");
	// 
	Для Н = 1 По мМесяцыЗадачи.Количество() Цикл
		СтрТЗ = тзПоМесяцам.Добавить();
		СтрТЗ.Месяц = мМесяцыЗадачи[Н-1];
		СтрТЗ.Сумма = 1;
	КонецЦикла;
	
	РаспределитьПоМесяцам( Статья, Сумма, тзПоМесяцам);
КонецФункции
	
// распределяем пропорционально фронтам работ по месяцам
// тзПоМесяцам - две колонки: Сумма и Месяц
Процедура РаспределитьПоМесяцам( Статья, Сумма, тзПоМесяцам = Неопределено, ДатаДляПустого = Неопределено, ПолеБазы = "Сумма" )
	ТипСуммы = Перечисления.ТипСуммыБюджета.План;
	
	Если тзПоМесяцам <> Неопределено Тогда
		Если тзПоМесяцам.Количество() <> 0 Тогда
			
			// коэффициенты 
			мКоэфф = Новый Массив;
			Для Каждого Стр ИЗ тзПоМесяцам Цикл
				//мКоэфф.Добавить( Стр.Сумма );
				мКоэфф.Добавить( Стр[ПолеБазы] );
			КонецЦикла;
			мРезПоСтатье = РаспределитьПропорционально( Сумма, мКоэфф);
			Если мРезПоСтатье <> Неопределено Тогда
			// может быть неопределено, если все коэффициенты = 0
				// теперь делаем движения по регистру Бюджет по месяцам 
				Для Каждого Стр ИЗ тзПоМесяцам Цикл
					Сумма = мРезПоСтатье[тзПоМесяцам.Индекс( Стр )];
					Если Сумма <> 0 Тогда
						Движение = Движения.БюджетПоМесяцам.Добавить();
						Движение.Период 		= Дата; // 
						Движение.ЗадачаПроекта 	= ЗадачаПроекта;
						Движение.СтатьяСметы 	= Статья;
						Движение.ТипСуммы 		= ТипСуммы; 
						Движение.Месяц			= НачалоМесяца( Стр.Месяц); // 
						// 
						Движение.Сумма			= Сумма;
					КонецЕсли;
				КонецЦикла;	
				Возврат;
				
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	Если ДатаДляПустого = Неопределено Тогда
		ДатаДляПустого = Дата( ЗадачаПроекта.Владелец.ГодПроекта, 1, 1);
	КонецЕсли;
		
	// если нет разбивки
	Движение = Движения.БюджетПоМесяцам.Добавить();
	Движение.Период 		= Дата;
	Движение.ЗадачаПроекта 	= ЗадачаПроекта;
	Движение.СтатьяСметы 	= Статья;
	Движение.ТипСуммы 		= ТипСуммы; 
	Движение.Месяц			= НачалоМесяца( ДатаДляПустого ); // 
	Движение.Сумма			= Сумма;
	
КонецПроцедуры

// ПП и прочие прямые расходы
Процедура РаспределитьПоДвижениямПервичныхДокументов( Статья, Сумма  )
	Запрос = Новый Запрос;
	// исправил 2014 08 12
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	СУММА(БюджетПоМесяцам.Сумма) КАК Сумма,
		|	НАЧАЛОПЕРИОДА(БюджетПоМесяцам.Месяц, МЕСЯЦ) КАК Месяц
		|ИЗ
		|	РегистрНакопления.БюджетПоМесяцам КАК БюджетПоМесяцам
		|ГДЕ
		|	БюджетПоМесяцам.ЗадачаПроекта = &ЗадачаПроекта
		|	И БюджетПоМесяцам.ТипСуммы = &ТипСуммы
		|	И БюджетПоМесяцам.СтатьяСметы = &СтатьяСметы
		|
		|СГРУППИРОВАТЬ ПО
		|	НАЧАЛОПЕРИОДА(БюджетПоМесяцам.Месяц, МЕСЯЦ)";

		
	Запрос.УстановитьПараметр("ТипСуммы", 		Перечисления.ТипСуммыБюджета.ПервичныйДокумент);
	Запрос.УстановитьПараметр("ЗадачаПроекта", 	ЗадачаПроекта);
	Запрос.УстановитьПараметр("СтатьяСметы", 	Статья);
	Результат = Запрос.Выполнить().Выгрузить();
	Результат.Сортировать("Месяц");
	// 	
	РаспределитьПоМесяцам( Статья, Сумма, Результат);
	
КонецПроцедуры


//
Функция СуммаДоходаФинансового()	
	Стр = Расчет.Найти( Справочники.СтатьиСметы.ДохФинансовые, "Статья");
	Если Стр = Неопределено Тогда Возврат 0; КонецЕсли;
	Возврат Стр.Сумма;
КонецФункции


// из уже сделанных движений отбираем по статье - базе распределения
Функция ПолучитьДвиженияПоСтатье( ДвиженияБюджетПоМесяцам, СтатьяОтбора )
	тз = ДвиженияБюджетПоМесяцам.Выгрузить(,"СтатьяСметы,Месяц,Сумма");
	Отбор = Новый Структура("СтатьяСметы", СтатьяОтбора);
	Возврат тз.Скопировать( Отбор );
КонецФункции



Функция ПолучитьРаспределениеПоСтатье( Статья,  ГодПроекта)
	Отбор = Новый Структура("СтатьяСметы", Статья );
	СтрРаспределения = РегистрыСведений.РаспределениеСтатейПоМесяцам.ПолучитьПоследнее( Дата( ГодПроекта, 1, 1));
	Возврат СтрРаспределения;
КонецФункции

// надо сделать в статье реквизит тип распределения !!
// 25 06 2017
Процедура ОбработкаПроведения(Отказ, Режим)
	//
	Проект		= ЗадачаПроекта.Владелец;
	ТипСуммы	= Перечисления.ТипСуммыБюджета.План; // план по смете задачи, т.е
	ГодПроекта 	= Проект.ГодПроекта;
	Если НЕ ЗначениеЗаполнено( ГодПроекта ) Тогда
		Отказ = Истина;
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = "В смете " + Номер + " от " + Дата + " Для проекта "+ Проект + " не указан год ";
		Сообщение.Сообщить();
		Возврат;
	КонецЕсли;
	// распределение фот по месяцам
	
	// сумма дохода финансового для учета
	СуммаДФ = СуммаДоходаФинансового();
	
	// указываем сумму по смете, т.к. смета задачи может проводиться первый раз
	// распределение с учетом этапов оплаты задачи проекта
	тзПланДоходов 	= РаспределениеДоходовПоЗадачеПроекта( ЗадачаПроекта, СуммаДФ );
	
	//********************************************************************************
	Движения.БюджетПоМесяцам.Записывать = Истина;
	Для Каждого ТекСтрокаРасходыЗадачиПроекта Из Расчет Цикл
		
		// итоговые статьи пропускаем, они будут формироваться в отчете
		Если ТекСтрокаРасходыЗадачиПроекта.Итоговая Тогда Продолжить; КонецЕсли;
		
		Статья				= ТекСтрокаРасходыЗадачиПроекта.Статья;
		СтрРаспределения 	= ПолучитьРаспределениеПоСтатье( Статья,  ГодПроекта);
		СпособРаспределения = СтрРаспределения.СпособРаспределения;
		
		Сумма 				= ТекСтрокаРасходыЗадачиПроекта.Сумма;
		Если Сумма = 0 Тогда
			Продолжить;
		КонецЕсли;
		
		Если ТекСтрокаРасходыЗадачиПроекта.ИзмененоДокументом Тогда
		// 2017 06 09 - если заполнено по документам - то движения ТОЛЬКО по первичным документам
			РаспределитьПоДвижениямПервичныхДокументов( Статья, Сумма );
			
		Иначе
			// если документов нет, то по типу распределения для статьи сметы
			// 1
			Если 	СпособРаспределения = Справочники.СпособРаспределенияСуммыПоМесяцам.ПоГрафикуОплатыДоговора Тогда
			// все пропорционально поступлению графику оплаты договора 
				РаспределитьПоМесяцам( Статья, Сумма, тзПланДоходов,, "Обеспечено" );
					
			// 4
			ИначеЕсли 	СпособРаспределения = Справочники.СпособРаспределенияСуммыПоМесяцам.РавномерноПоМесяцам Тогда
				// 2017 09 19
				РаспределитьРавномерноПоПериодуЗадачи( Статья, Сумма );
				
			ИначеЕсли СпособРаспределения = Справочники.СпособРаспределенияСуммыПоМесяцам.ПоСтатьеСметы Тогда
				// 
				Если ЗначениеЗаполнено( СтрРаспределения.БазоваяСтатьяСметы ) Тогда
					тзДвиженияБазы = ПолучитьДвиженияПоСтатье( Движения.БюджетПоМесяцам, СтрРаспределения.БазоваяСтатьяСметы );
					РаспределитьПоМесяцам( Статья, Сумма, тзДвиженияБазы );
				Иначе
				// если выбран способ по статье и статья не указана, то по доходам 
					РаспределитьПоМесяцам( Статья, Сумма, тзПланДоходов,, "Обеспечено" );
					
					Сообщение = Новый СообщениеПользователю;
					Сообщение.Текст = "Для статьи " + Статья + " не указана базовая статья распределения";
					Сообщение.Сообщить();
				КонецЕсли;					
				
				
			Иначе				
				Сообщение = Новый СообщениеПользователю;
				Сообщение.Текст = "Для статьи " + Статья + " не указан способ распределения по месяцам";
				Сообщение.Сообщить();
			
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры


Процедура ОбработкаУдаленияПроведения(Отказ)

	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	СметаДокументы.Ссылка КАК Смета,
		|	СметаДокументы.Ссылка.Проект
		|ИЗ
		|	Документ.Смета.Документы КАК СметаДокументы
		|ГДЕ
		|	СметаДокументы.Документ = &Смета
		|	И СметаДокументы.Ссылка.Проведен";

	Запрос.УстановитьПараметр("Смета", 		Ссылка );

	Результат = Запрос.Выполнить();

	ВыборкаДетальныеЗаписи = Результат.Выбрать();

	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		Сообщить("Текущая смета задачи включена в смету " + 
					ВыборкаДетальныеЗаписи.Смета + ", проекта " +
					ВыборкаДетальныеЗаписи.Проект );
		Отказ = Истина;
		Возврат;
	КонецЦикла;

КонецПроцедуры

// 2013,07,03
// проверка осуществляется только при интерактивном проведении, т.е. в форме
// т.к. при проведении программном ( из документа Договор )
// проводится должно не зависимо от статуса проекта
Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	Если УП_ПереносДанных.ПроведениеПриПереносе() Тогда
		Возврат;
	КонецЕсли;
	
	
	//Если РежимЗаписи = РежимЗаписиДокумента.Проведение 
	//И НЕ РазрешеноПроводитьДокумент( Ссылка, Ссылка.ЗадачаПроекта.Владелец ) Тогда
	//	Отказ = Истина;
	//	Возврат;
	//КонецЕсли;		

КонецПроцедуры
